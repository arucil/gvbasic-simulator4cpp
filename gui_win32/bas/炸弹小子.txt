0 :
20 DIM MAP%(15,7),Z%(15,7):A$="白方":B$="黑方"
60 FOR I=1  TO 14 STEP 2:FOR J=1  TO 6 STEP 2:MAP%(I,J)=1:NEXT:NEXT
100 FOR I=0  TO 14:FOR J=0  TO 6:Z%(I,J)=0:IF MAP%(I,J)=1 THEN 130
120 IF RND(1)<0.35 THEN MAP%(I,J)=0:130
121 MAP%(I,J)=2:RN=INT(RND(1)*13):IF RN=0 THEN Z%(I,J)=4 ELSE IF RN=1 THEN Z%(I,J)=5
130 NEXT:NEXT
150 Z%(0,0)=0:MAP%(0,0)=7:Z%(0,1)=0:MAP%(0,1)=0:Z%(1,0)=0:MAP%(1,0)=0
160 Z%(14,6)=0:MAP%(14,6)=8:Z%(14,5)=0:MAP%(14,5)=0:Z%(13,6)=0:MAP%(13,6)=0:TX=1:TY=0
170 FOR I=0  TO 7:ZZ(I)=0:ZD(I)=0:NEXT
200 AA=1:BB=1:AX=0:AY=0:BX=14:BY=6:AZ=1:BZ=1:AW=1:BW=1:AZZ=0:BZZ=0:WIA=0:WIB=0
230 GRAPH:BOX 2,2,157,77:BOX 3,3,156,76:BOX 1,0,159,79
240 FOR I=0  TO 14:FOR J=0  TO 6:PX=I:PY=J
250 IF MAP%(I,J)=0 THEN GOSUB 310
260 IF MAP%(I,J)=1 THEN GOSUB 330
270 IF MAP%(I,J)=2 THEN GOSUB 390
280 IF MAP%(I,J)=7 THEN GOSUB 560
290 IF MAP%(I,J)=8 THEN GOSUB 590
300 NEXT:NEXT:GOTO 860
310 BOX PX*10+5,PY*10+5,PX*10+14,PY*10+9+5,1,0
320 RETURN
330 BOX PX*10+5,PY*10+5,PX*10+14,PY*10+14,1,0
340 BOX PX*10+5,PY*10+5,PX*10+14,PY*10+14
350 BOX PX*10+8,PY*10+8,PX*10+11,PY*10+11:LINE PX*10+5,PY*10+5,PX*10+8,PY*10+8
360 LINE PX*10+5,PY*10+14,PX*10+8,PY*10+11:LINE PX*10+14,PY*10+14,PX*10+11,PY*10+11
370 LINE PX*10+11,PY*10+8,PX*10+14,PY*10+5:RETURN
390 BOX PX*10+5,PY*10+5,PX*10+14,PY*10+14,1,0
400 BOX PX*10+5,PY*10+5,PX*10+14,PY*10+14:BOX PX*10+6,PY*10+6,PX*10+14,PY*10+14
410 LINE PX*10+5,PY*10+8,PX*10+14,PY*10+8:LINE PX*10+5,PY*10+10,PX*10+14,PY*10+10
420 LINE PX*10+5,PY*10+12,PX*10+14,PY*10+12:RETURN
440 BOX PX*10+5,PY*10+5,PX*10+14,PY*10+14,1,0
450 CIRCLE PX*10+10,PY*10+11,3,1:LINE PX*10+10,PY*10+6,PX*10+10,PY*10+7
460 LINE PX*10+10,PY*10+6,PX*10+14,PY*10+5:DRAW PX*10+12,PY*10+10,0:RETURN
470 BOX PX*10+5,PY*10+5,PX*10+14,PY*10+14,1,0
480 BOX PX*10+8,PY*10+6,PX*10+11,PY*10+11
491 LINE PX*10+7,PY*10+12,PX*10+12,PY*10+12:LINE PX*10+6,PY*10+13,PX*10+13,PY*10+13:RETURN
500 BOX PX*10+5,PY*10+5,PX*10+14,PY*10+14,1,0:CIRCLE PX*10+10,PY*10+10,4,1
520 LINE PX*10+10,PY*10+7,PX*10+10,PY*10+13,0
521 LINE PX*10+7,PY*10+10,PX*10+13,PY*10+10,0:RETURN
530 BOX PX*10+5,PY*10+5,PX*10+14,PY*10+14,1,1
550 RETURN
560 BOX PX*10+5,PY*10+5,PX*10+14,PY*10+14,1,0
570 CIRCLE PX*10+10,PY*10+8,3:LINE PX*10+9,PY*10+11,PX*10+6,PY*10+14
580 LINE PX*10+10,PY*10+11,PX*10+14,PY*10+14:LINE PX*10+10,PY*10+11,PX*10+10,PY*10+14
581 LINE PX*10+6,PY*10+14,PX*10+14,PY*10+14:DRAW PX*10+9,PY*10+8:DRAW PX*10+11,PY*10+8
582 RETURN
590 BOX PX*10+5,PY*10+5,PX*10+14,PY*10+14,1,0
600 CIRCLE PX*10+10,PY*10+8,3,1:LINE PX*10+9,PY*10+11,PX*10+6,PY*10+14
610 LINE PX*10+10,PY*10+11,PX*10+14,PY*10+14:LINE PX*10+10,PY*10+11,PX*10+10,PY*10+14
611 LINE PX*10+6,PY*10+14,PX*10+14,PY*10+14:DRAW PX*10+9,PY*10+8,0
612 DRAW PX*10+11,PY*10+8,0 :RETURN
700 NX=AX+TX:NY=AY+TY:IF NX<0 OR NX>14 OR NY<0 OR NY>6 THEN 760
705 TT=MAP%(NX,NY):IF TT=1 OR TT=2 OR TT=3 OR TT=8 THEN 760
710 I=0:WHILE I<4:IF ZZ(I)=0 OR ZZ(I)=2 THEN 730
720 IF ZX(I)=AX AND ZY(I)=AY THEN PX=AX:PY=AY:GOSUB 440:MAP%(AX,AY)=3
730 I=I+1:WEND:IF MAP%(AX,AY)=7 THEN MAP%(AX,AY)=0:PX=AX:PY=AY:GOSUB 310
740 AX=NX:AY=NY:TT=MAP%(AX,AY)
741 IF TT=4 THEN Z%(AX,AY)=0:IF AW<4 THEN AW=AW+1
742 IF TT=5 THEN Z%(AX,AY)=0:IF AZ<4 THEN AZ=AZ+1
743 IF TT>40 THEN AA=0
750 PX=AX:PY=AY:GOSUB 560:MAP%(AX,AY)=7
760 RETURN
770 NX=BX+NX:NY=BY+NY:IF NX<0 OR NX>14 OR NY<0 OR NY>6 THEN 850
780 TT=MAP%(NX,NY):IF TT=1 OR TT=2 OR TT=3 OR TT=7 THEN 850
790 I=4:WHILE I<8:IF ZZ(I)=0 OR ZZ(I)=2 THEN 810
800 IF ZX(I)=BX AND ZY(I)=BY THEN PX=BX:PY=BY:GOSUB 440:MAP%(BX,BY)=3
810 I=I+1:WEND:IF MAP%(BX,BY)=8 THEN MAP%(BX,BY)=0 :PX=BX:PY=BY:GOSUB 310 
820 BX=NX:BY=NY:TT=MAP%(BX,BY)
830 IF TT=4 THEN Z%(BX,BY)=0:IF BW<4 THEN BW=BW+1
831 IF TT=5 THEN Z%(BX,BY)=0:IF BZ<4 THEN BZ=BZ+1
832 IF TT>9 THEN BB=0
840 PX=BX:PY=BY:GOSUB 590:MAP%(BX,BY)=8
850 RETURN
860 X=AX:Y=AY:GOSUB 5000:IF EA=0 THEN 5510 ELSE 5310
930 I=0:PP=0:WHILE PP=0 AND I<4:IF ZZ(I)=1 AND ZX(I)=AX AND ZY(I)=AY THEN PP=1:950
935 IF ZZ(I)>0 THEN 950
940 AZZ=AZZ+1:ZX(I)=AX:ZY(I)=AY:ZZ(I)=1:PP=1:ZW(I)=AW:ZD(I)=0
950 I=I+1:WEND:GOTO 5540
955 GOSUB 700
957 IF PEEK(199)<128 THEN 1040
958 K=ASC(INKEY$)
960 IF K=20 THEN NX=0:NY=-1:GOSUB 770
970 IF K=21 THEN NX=0:NY=1:GOSUB 770
980 IF K=23 THEN NX=-1:NY=0:GOSUB 770
990 IF K=22 THEN NX=1:NY=0:GOSUB 770
1000 IF K<>13 OR BZZ=BZ THEN 1040
1010 I=4:PP=0:WHILE PP=0 AND I<8:IF ZZ(I)=1 AND ZX(I)=BX AND ZY(I)=BY THEN PP=1:1030
1015 IF ZZ(I)>0 THEN 1030
1020 BZZ=BZZ+1:ZX(I)=BX:ZY(I)=BY:ZZ(I)=1:PP=1:ZW(I)=BW:ZD(I)=0
1030 I=I+1:WEND
1040 sleep 80:FOR I=0  TO 7:WW=ZW(I)
1050 IF ZZ(I)=0 THEN 1630
1060 IF ZZ(I)=1 THEN 1260
1080 ZD(I)=0:ZZ(I)=0:FOR J=0  TO WW:IF ZY(I)-J<0 THEN 1130
1090 T=MAP%(ZX(I),ZY(I)-J):IF T<10 THEN 1130
1100 T=Z%(ZX(I),ZY(I)-J):MAP%(ZX(I),ZY(I)-J)=T
1110 PX=ZX(I):PY=ZY(I)-J:IF T=4 THEN GOSUB 470 ELSE IF T=5 THEN GOSUB 500 ELSE GOSUB 310
1130 IF ZY(I)+J>6 THEN 1170
1140 T=MAP%(ZX(I),ZY(I)+J):IF T<10 THEN 1170
1150 T=Z%(ZX(I),ZY(I)+J):MAP%(ZX(I),ZY(I)+J)=T
1160 PX=ZX(I):PY=ZY(I)+J:IF T=4 THEN GOSUB 470 ELSE IF T=5 THEN GOSUB 500 ELSE GOSUB 310
1170 IF ZX(I)-J<0 THEN 1210
1180 T=MAP%(ZX(I)-J,ZY(I)):IF T<10 THEN 1210
1190 T=Z%(ZX(I)-J,ZY(I)):MAP%(ZX(I)-J,ZY(I))=T
1200 PX=ZX(I)-J:PY=ZY(I):IF T=4 THEN GOSUB 470 ELSE IF T=5 THEN GOSUB 500 ELSE GOSUB 310
1210 IF ZX(I)+J>14 THEN 1250
1220 T=MAP%(ZX(I)+J,ZY(I)):IF T<10 THEN 1250
1230 T=Z%(ZX(I)+J,ZY(I)):MAP%(ZX(I)+J,ZY(I))=T
1240 PX=ZX(I)+J:PY=ZY(I):IF T=4 THEN GOSUB 470 ELSE IF T=5 THEN GOSUB 500 ELSE GOSUB 310
1250 NEXT:GOTO 1630
1260 IF ZD(I)<10 THEN ZD(I)=ZD(I)+1:1630
1265 IF I<4 THEN AZZ=AZZ-1 ELSE BZZ=BZZ-1
1270 ZZ(I)=2:ZD(I)=0:U=WW:D=U:L=U:R=U:MAP%(ZX(I),ZY(I))=0
1273 FOR J=0  TO WW:IF ZY(I)-J<0 OR J>U THEN 1360
1280 PX=ZX(I):PY=ZY(I)-J:T=MAP%(PX,PY)
1285 IF T=1 THEN U=J:1360 ELSE IF T=2 THEN U=J:1300 ELSE IF T=3 THEN 1340
1295 Z%(PX,PY)=0
1300 MAP%(PX,PY)=(I+1)*10
1310 GOSUB 530:GOTO 1360
1340 FOR M=0  TO 7:IF ZX(M)=PX AND ZY(M)=PY THEN ZD(M)=35:U=J
1350 NEXT:GOTO 1310
1360 IF ZY(I)+J>6 OR J>D THEN 1450
1370 PX=ZX(I):PY=ZY(I)+J:T=MAP%(PX,PY):IF T=1 THEN D=J:1450 ELSE IF T=3 THEN 1430
1380 IF T=2 THEN D=J:1390
1385 Z%(PX,PY)=0
1390 MAP%(PX,PY)=(I+1)*10
1400 GOSUB 530:GOTO 1450
1430 FOR M=0  TO 7:IF ZX(M)=PX AND ZY(M)=PY THEN ZD(M)=35:D=J
1440 NEXT:GOTO 1400
1450 IF ZX(I)-J<0 OR J>L THEN 1540
1460 PX=ZX(I)-J:PY=ZY(I):T=MAP%(PX,PY):IF T=1 THEN L=J:1540 ELSE IF T=3 THEN 1520
1470 IF T=2 THEN L=J:1480
1475 Z%(PX,PY)=0
1480 MAP%(PX,PY)=(I+1)*10
1490 GOSUB 530:GOTO 1540
1520 FOR M=0  TO 7:IF ZX(M)=PX AND ZY(M)=PY THEN ZD(M)=35:L=J
1530 NEXT:GOTO 1490
1540 IF ZX(I)+J>14 OR J>R THEN 1630
1550 PX=ZX(I)+J:PY=ZY(I):T=MAP%(PX,PY):IF T=1 THEN R=J:1630 ELSE IF T=3 THEN 1610
1560 IF T=2 THEN R=J:1570
1565 Z%(PX,PY)=0
1570 MAP%(PX,PY)=(I+1)*10
1580 GOSUB 530:GOTO 1630
1610 FOR M=0  TO 7:IF ZX(M)=PX AND ZY(M)=PY THEN ZD(M)=35:R=J
1620 NEXT:GOTO 1580
1630 NEXT
1640 IF MAP%(AX,AY)>40 THEN AA=0
1650 IF MAP%(BX,BY)>9 THEN BB=0
1660 IF AA=0 THEN WIB=1
1670 IF BB=0 THEN WIA=1 
1680 IF WIB>0 OR WIA>0 THEN 2000
1690 GOTO 860
2000 IF WIA=0 OR WIB=0 THEN 2010 ELSE PIN=PIN+1:LOCATE 2,6:PRINT A$;"玩家与":LOCATE 3,6
2001 PRINT B$;"玩家  ":LOCATE 4,6:PRINT "战成平手":BOX 40,15,120,64:GOTO 2030
2010 IF WIA=1 THEN AWR=AWR+1:BLR=BLR+1:N$=A$ ELSE ALR=ALR+1:BWR=BWR+1:N$=B$ 
2020 LOCATE 3,4:PRINT N$;"玩家获胜":BOX 24,31,136,48
2030 K$=INKEY$:K$=INKEY$:CLS
2040 LOCATE 2,1:PRINT "白方玩家:胜";AWR;"败";ALR,"黑方玩家:胜";BWR;"败";BLR,"双方平局:";PIN
2050 K$=INKEY$:GOTO 60
4900 FOR J=4  TO 7:IF ZX(J)=PX AND ZY(J)=PY AND ZW(J)+1>I AND ZD(J)>2 THEN NN=1
4910 NEXT:RETURN
5000 EA=0:UA=0:DA=0:LA=0:RA=0:I=1:WHILE I<5 AND EA=0
5010 IF UA=1 THEN 5060
5020 PX=X:PY=Y-I:IF PY<0 THEN UA=1:5060
5030 PP=MAP%(PX,PY):IF PP<>3 THEN 5050
5040 NN=0:GOSUB 4900:IF NN=1 THEN UU=5:LL=0:RR=0:DD=I:EA=I+1:5210
5050 IF PP=1 OR PP=2 THEN UA=1
5060 IF DA=1 THEN 5110
5070 PX=X:PY=Y+I:IF PY>6 THEN DA=1:5110
5080 PP=MAP%(PX,PY):IF PP<>3 THEN 5100
5090 NN=0:GOSUB 4900:IF NN=1 THEN DD=5:LL=0:RR=0:UU=I:EA=I+1:5210
5100 IF PP=1 OR PP=2 THEN DA=1
5110 IF LA=1 THEN 5160
5120 PX=X-I:PY=Y:IF PX<0 THEN LA=1:5160
5130 PP=MAP%(PX,PY):IF PP<>3 THEN 5150
5140 NN=0:GOSUB 4900:IF NN=1 THEN LL=5:UU=0:DD=0:RR=I:EA=I+1:5210
5150 IF PP=1 OR PP=2 THEN LA=1
5160 IF RA=1 THEN 5210
5170 PX=X+I:PY=Y:IF PX>14 THEN RA=1:5210
5180 PP=MAP%(PX,PY):IF PP<>3 THEN 5200
5190 NN=0:GOSUB 4900:IF NN=1 THEN RR=5:UU=0:DD=0:LL=I:EA=I+1:5210
5200 IF PP=1 OR PP=2 THEN RA=1
5210 I=I+1:WEND
5220 RETURN
5230 IF TAX<0 OR TAX>14 OR TAY<0 OR TAY>6 THEN TA=1 ELSE TA=MAP%(TAX,TAY)
5240 IF TBX<0 OR TBX>14 OR TBY<0 OR TBY>6 THEN TB=1 ELSE TB=MAP%(TBX,TBY)
5250 IF TCX<0 OR TCX>14 OR TCY<0 OR TCY>6 THEN TC=1 ELSE TC=MAP%(TCX,TCY)
5260 IF TA=0 OR TA=4 OR TA=5 OR (TA>9 AND TA<41) THEN 5270 ELSE NA=1:5300
5270 IF I>NB THEN NN=0
5280 IF TB=0 OR TB=4 OR TB=5 OR TC=0 OR TC=4 OR TC=5 THEN NN=0
5290 IF (TB>9 AND TB<41) OR (TC>9 AND TC<41) THEN NN=0
5300 RETURN
5310 U=1:D=1:L=1:R=1:UA=0:DA=0:LA=0:RA=0:I=1:WHILE I<=EA
5320 IF U=0 OR UA=1 THEN 5350
5330 NN=U:NA=UA:NB=UU:TAX=AX:TAY=AY-I:TBX=AX-1:TBY=AY-I:TCX=AX+1:TCY=AY-I:GOSUB 5230
5340 U=NN:UA=NA
5350 IF D=0 OR DA=1 THEN 5380
5360 NN=D:NA=DA:NB=DD:TAX=AX:TAY=AY+I:TBX=AX-1:TBY=AY+I:TCX=AX+1:TCY=AY+I:GOSUB 5230
5370 D=NN:DA=NA
5380 IF L=0 OR LA=1 THEN 5410
5390 NN=L:NA=LA:NB=LL:TAX=AX-I:TAY=AY:TBX=AX-I:TBY=AY-1:TCX=AX-I:TCY=AY+1:GOSUB 5230
5400 L=NN:LA=NA
5410 IF R=0 OR RA=1 THEN 5440
5420 NN=R:NA=RA:NB=RR:TAX=AX+I:TAY=AY:TBX=AX+I:TBY=AY-1:TCX=AX+I:TCY=AY+1:GOSUB 5230
5430 R=NN:RA=NA
5440 I=I+1:WEND
5450 IF U=1 AND D=1 AND L=1 AND R=1 THEN 955 ELSE GOSUB 5460:GOTO 5550
5460 RN=INT(RND(1)*4)
5470 IF RN=0 AND (U=0 OR PC=1) THEN TX=0:TY=-1
5475 IF RN=1 AND (D=0 OR PC=1) THEN TX=0:TY=1
5480 IF RN=2 AND (L=0 OR PC=1) THEN TX=-1:TY=0
5485 IF RN=3 AND (R=0 OR PC=1) THEN TX=1:TY=0
5486 IF PC=1 THEN 5500 ELSE X=AX+TX:Y=AY+TY
5487 IF X<0 OR Y<0 OR X>14 OR Y>6 THEN T=1 ELSE T=MAP%(X,Y)
5488 IF T<>0 AND T<>4 AND T<>5 AND (T<10 OR T>40) THEN 5460 ELSE 5500
5490 GOTO 5460 
5500 RETURN
5510 TAX=AX+TX:TAY=AY+TY:IF TAX<0 OR TAY<0 OR TAX>14 OR TAY>6 THEN TA=1 ELSE TA=MAP%(TAX,TAY)
5520 IF RND(1)>0.2 AND (TA=0 OR TA=4 OR TA=5 OR (TA>9 AND TA<41)) THEN 5540
5530 PC=1:GOSUB 5460:PC=0:IF AZZ<AZ THEN 930 
5540 X=AX+TX:Y=AY+TY:IF X<0 OR X>14 OR Y<0 OR Y>6 THEN 5550
5545 GOSUB 5000:IF EA>0 OR MAP%(X,Y)>40 THEN TX=0:TY=0
5550 GOTO 955