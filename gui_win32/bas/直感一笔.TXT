0 :
10 GRAPH
20 DIM M(10,10),D(10,10),H(50),BX(100),BY(100),B(20)
30 PRINT "【直感一笔】 V 1.0",TAB (13);"关卡模式",TAB (13);"键位说明",TAB (13);"退出游戏"
40 PRINT "Made by Unicors";
50 A=1:B=1
60 BOX 80,A*16-1,160,A*16+15,1,2:BOX 1,A*16+5,84,A*16+9,1,2
70 Q=ASC(INKEY$)
80 IF Q=20 THEN IF A=1 THEN A=3:120 ELSE A=A-1:120
90 IF Q=21 THEN IF A=3 THEN A=1:120 ELSE A=A+1:120
100 IF Q=13 THEN 130
110 GOTO 70
120 BOX 80,B*16-1,160,B*16+15,1,2:BOX 1,B*16+5,84,B*16+9,1,2:B=A: GOTO 60
130 ON A GOTO 150,1250,140
140 CLS:PRINT "ＳＥＥ  ＹＯＵ":END
150 OPEN "Intui-save" FOR RANDOM AS#1
160 IF LOF(1) THEN CLOSE #1:220 ELSE CLOSE #1:170
170 OPEN "Intui-save" FOR OUTPUT AS#1
180 WRITE #1,0
190 FOR I=1 TO 50:WRITE #1,99:NEXT 
200 CLOSE #1
220 OPEN "Intui-save" FOR INPUT AS#1:INPUT #1,HLV
230 FOR I=1 TO 50:INPUT #1,H(I):NEXT 
240 CLOSE #1
250 CLS
260 FOR I=1 TO 50
270 IF HLV>=I THEN BOX I*3+3,5,I*3+4,10,1,1 ELSE LINE I*3+3,5,I*3+4,5
280 NEXT 
290 BOX 4,3,156,12,0,1
300 IF HLV=50 THEN A=1 ELSE A=HLV+1
310 BOX A*3+3,14,A*3+4,15
320 LOCATE 3,1:PRINT "第";A;"关";" "
330 LOCATE 4,1:PRINT "最低步数:";H(A);"    "
340 Q=ASC(INKEY$)
350 IF Q=22 THEN IF A=HLV+1 OR A=50 THEN A=1:400 ELSE A=A+1:400
360 IF Q=23 THEN IF A=1 THEN A=HLV+1:400 ELSE A=A-1:400
370 IF Q=13 GOTO 420
380 IF Q=27 THEN CLS: GOTO 30
390 GOTO 340
400 IF A=51 THEN A=50
410 BOX 1,14,160,15,1,0: GOTO 310
420 LOCATE 5,1:PRINT "[LOADING] ";
500 OPEN "Intui-maps" FOR RANDOM AS#1 LEN=1:FIELD #1,1 AS A$
510 LV=A:A=A*21-20
520 GET #1,A:BZ=ASC(A$)
530 FOR I=1 TO 20
540 GET #1,A+I:B(I)=ASC(A$):NEXT :CLOSE #1
550 FOR I=1 TO 10:A(I)=B(I*2-1)+B(I*2)*256:NEXT :ZS=0:MS=1
560 FOR Y=1 TO 10
570 FOR X=1 TO 10
580 IF A(Y)/2=INT(A(Y)/2) THEN M(X,Y)=0 ELSE M(X,Y)=1:ZS=ZS+1
590 A(Y)=INT(A(Y)/2)
600 NEXT X:NEXT Y
610 CLS: FOR Y=1 TO 10: FOR X=1 TO 10
620 IF M(X,Y)=1 THEN BOX X*7+78,Y*7-2,X*7+84,Y*7+4,1,1
630 NEXT X:NEXT Y
640 BOX 83,3,156,76,0,1:BOX 81,1,158,78,0,1
650 IF MS=1 THEN PRINT "【第";LV;"关】" ELSE PRINT "[随机模式]"
660 PRINT "标准:";BZ;"笔","记录:";H(LV);"笔","剩余:";ZS,"笔数:0";
670 X=1:Y=1:XA=1:YA=1:BS=0
680 BOX X*7+80,Y*7,X*7+82,Y*7+2,1,2
690 Q=ASC(INKEY$)
700 IF Q=20 THEN IF Y=1 THEN Y=10:780 ELSE Y=Y-1:780
710 IF Q=21 THEN IF Y=10 THEN Y=1:780 ELSE Y=Y+1:780
720 IF Q=22 THEN IF X=10 THEN X=1:780 ELSE X=X+1:780
730 IF Q=23 THEN IF X=1 THEN X=10:780 ELSE X=X-1:780
740 IF Q=13 GOTO 840
750 IF Q=27 GOTO 790
760 IF Q=28 GOTO 820
770 GOTO 690
780 BOX XA*7+80,YA*7,XA*7+82,YA*7+2,1,2:XA=X:YA=Y: GOTO 680
790 LOCATE 5,1:PRINT "退出?(Y/N)";
800 Q$=INKEY$:IF Q$="y" THEN CLS: GOTO 250
810 LOCATE 5,1:PRINT "          ";:LOCATE 5,1:PRINT "笔数:";BS;: GOTO 690
820 LOCATE 5,1:PRINT "重来?(Y/N)";
830 Q$=INKEY$:IF Q$="y" THEN A=LV:420 ELSE 810
840 BS=BS+1:LOCATE 5,6:PRINT BS;
850 MX=X:MY=Y:NX=X:NY=Y:B=1:BX(1)=X:BY(1)=Y:D(X,Y)=1
860 Q=ASC(INKEY$)
870 IF Q=20 THEN IF MY=1 THEN 860 ELSE MY=MY-1:930
880 IF Q=21 THEN IF MY=10 THEN 860 ELSE MY=MY+1:930
890 IF Q=22 THEN IF MX=10 THEN 860 ELSE MX=MX+1:930
900 IF Q=23 THEN IF MX=1 THEN 860 ELSE MX=MX-1:930
910 IF Q=13 GOTO 1030
920 GOTO 860
930 IF D(MX,MY)=1 THEN MX=NX:MY=NY:860 ELSE D(MX,MY)=1
935 IF MX-NX=1 GOTO 970
940 IF NX-MX=1 GOTO 980
950 IF MY-NY=1 GOTO 990
960 XM=MX*7+80:YM=MY*7:XN=NX*7+82:YN=NY*7-1: GOTO 1000
970 XM=NX*7+83:YM=NY*7:XN=MX*7+82:YN=MY*7+2: GOTO 1000
980 XM=MX*7+80:YM=MY*7:XN=NX*7+79:YN=NY*7+2: GOTO 1000
990 XM=NX*7+80:YM=NY*7+3:XN=MX*7+82:YN=MY*7+2
1000 BOX XM,YM,XN,YN,1,2
1010 B=B+1:BX(B)=MX:BY(B)=MY:NX=MX:NY=MY
1020 GOTO 860
1030 FOR I=1 TO B
1040 M=BX(I):N=BY(I):D(M,N)=0
1050 IF M(M,N)=1 THEN M(M,N)=0:ZS=ZS-1 ELSE M(M,N)=1:ZS=ZS+1
1060 BOX M*7+78,N*7-2,M*7+84,N*7+4,1,M(M,N)
1070 NEXT 
1080 LOCATE 4,6:PRINT ZS;" ";
1090 IF ZS=0 GOTO 1110
1100 X=MX:Y=MY:XA=X:YA=Y: GOTO 680
1110 FOR I=1 TO 40
1120 BOX I+79,I-1,161-I,81-I,0,1:NEXT 
1130 FOR I=1 TO 40
1140 BOX I+79,I-1,161-I,81-I,0,0:NEXT 
1150 LOCATE 3,11:PRINT "Finished!":BOX 78,32,152,48,1,2
1160 Q$=INKEY$:CLS
1170 IF BS-INT(SQR(BZ)-0.5)<=BZ THEN 1190
1180 PRINT "非常遗憾…你用的笔数太多了","不能通过此关 >_<","请下次再努力!": GOTO 1210
1190 PRINT "恭喜恭喜!","顺利过关!"
1200 IF LV=HLV+1 THEN HLV=HLV+1
1210 IF BS<H(LV) THEN H(LV)=BS
1215 OPEN "Intui-save" FOR OUTPUT AS#1:WRITE #1,HLV
1220 FOR I=1 TO 50:WRITE #1,H(I):NEXT :CLOSE #1
1230 Q$=INKEY$
1235 IF HLV=50 GOTO 1290
1240 GOTO 250
1250 CLS
1260 PRINT "[方向键]控制移动","[输入]开始画线","[F1]重来","[跳出]退出"
1270 Q$=INKEY$
1280 CLS: GOTO 30
1290 CLS:PRINT "你通关了!!","这么难的游戏你竟然通关了!","你真是个天才!!"
1300 Q$=INKEY$
1310 GOTO 250