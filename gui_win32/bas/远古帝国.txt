0 :
1 DIM X(8,36):DIM Y(8,36):DIM Z(19,7):DIM Q(19,7):DIM M(15):DIM N(15)
2 DIM MU(50):DIM KU(30):SA=10:SB=4
3 C=-1:FS=3
8 PRINT "Waiting...":OPEN "EmpireDATA" FOR INPUT AS #1:FOR AA=1  TO 8
9 INPUT #1,JI(AA):FOR BB=1  TO JI(AA):INPUT #1,X(AA,BB),Y(AA,BB):NEXT:NEXT:CLOSE #1
10 GRAPH
12 DEF FN R(A)=INT(RND(1)*A+1):DEF FN ZF(A)=INT(A/1000)*300
15 PRINT "      远古帝国","      Conquer","       Load","       ExIt","  战 略     绝 对";
22 BOX 1,0,159,15,1,2:BOX 1,63,159,79,1,2
24 GOSUB 50:GOTO 60
30 A$=INKEY$:SU=ASC(A$):RETURN
40 sleep 40:RETURN
41 BOX 1,0,159,15,1,2:BOX 1,0,159,79:RETURN
42 FOR I1=1  TO 999:NEXT:RETURN
43 Y$="资金不足":GOSUB 145:RETURN
44 Y$="位置错误":GOSUB 145:RETURN
50 A=FN R(6):B=FN R(5)
55 BOX 13+A,29+B+C*15,137+A,44+B+C*15,0,2:RETURN
60 IF PEEK(199)<128 THEN GOSUB 40:GOSUB 55:GOTO 24
65 GOSUB 55
70 GOSUB 30:C=C-2*(20.5-SU)*(SU=20 OR SU=21):C=C+(C=-2)-(C=2)
75 IF SU=13 THEN 80 ELSE 24
80 CLS:ON C+2 GOTO 850,150,85
85 CLS:PRINT SPC(46);"GoodBye!!","   ";:END
90 BOX X*8,Y*8,X*8+7,Y*8+7,1,0:RETURN
95 GOSUB 90:FOR AB=1  TO JI(Z):DRAW X*8+X(Z,AB)-1,Y*8+Y(Z,AB)-1:NEXT:RETURN
100 BOX X*8,Y*8,X*8+7,Y*8+7,1,2:RETURN
105 LOCATE 5,1:PRINT SPC(19);:LOCATE 5,1:RETURN
110 Z(X,Y)=0:Q(X,Y)=0:GOSUB 90:RETURN
119 GOSUB 100 
120 GOSUB 105:PRINT "木->";MOA;" 金->";MOB;" ";5-XIN;:sleep 1000:GOTO 215
125 TEXT:INPUT "保存文件名:";MI$:IF MI$="" THEN 125
130 GRAPH:PRINT SPC(45);"Waiting..."
131 OPEN MI$ FOR OUTPUT AS #1:WRITE #1,XX$:FOR C=0  TO 19:FOR D=0  TO 7:WRITE #1,Z(C,D)
132 NEXT:NEXT:FOR C=0  TO 19:FOR D=0  TO 7:WRITE #1,Q(C,D):NEXT:NEXT
133 FOR C=1  TO 50:WRITE #1,MU(C):NEXT:FOR C=1  TO 30:WRITE #1,KU(C):NEXT
134 FOR C=1  TO 15:WRITE #1,M(C):NEXT:FOR C=1  TO 15:WRITE #1,N(C):NEXT
135 WRITE #1,MOA,MOB,BU,YI,GO,MAX,HH,XIN,DMO,J,MOS,H8,CH:FOR C=1  TO 9:WRITE #1,T(C):NEXT
136 CLOSE #1:CLS:PRINT SPC(48);"完成"
138 BEEP:GOSUB 30:GOTO 85
140 H8=H8+1:GOSUB 100:XIN=0:GOSUB 144:RX=X:RY=Y:GOTO 600
144 Y$="轮到敌方了":GOSUB 145:RETURN
145 GOSUB 105:PRINT Y$;:BEEP:GOSUB 42:GOSUB 42:GOSUB 105:RETURN
150 TEXT:INPUT "FiLe Name:";MIN$:IF MIN$="" THEN 150 ELSE GRAPH
153 CLS:CR=0:OPEN MIN$ FOR INPUT AS #1:INPUT #1,XX$
155 FOR AA=0  TO 19:FOR BB=0  TO 7:INPUT #1,Z(AA,BB)
156 IF Z(AA,BB)=4 THEN JX=AA:JY=BB ELSE IF Z(AA,BB)=5 THEN DIX=AA:DIY=BB
157 Z=Z(AA,BB)-(Z(AA,BB)>4):IF Z>0 THEN X=AA:Y=BB:GOSUB 95
158 NEXT:NEXT
159 GOSUB 105:PRINT "Waiting...";
160 FOR AA=0  TO 19:FOR BB=0  TO 7:INPUT #1,Q(AA,BB):NEXT:NEXT
161 FOR AA=1  TO 50:INPUT #1,MU(AA):CH=CH+(MU(AA)<>-1):NEXT:FOR AA=1  TO 30:INPUT #1,KU(AA)
162 CH=CH+(KU(AA)<>-1):NEXT:FOR AA=1  TO 15:INPUT #1,M(AA):NEXT:FOR AA=1  TO 15
163 INPUT #1,N(AA):NEXT:INPUT #1,MOA,MOB,BU,YI,GO,MAX,HH,XIN,DMO,J,MOS,H8,CH
164 FOR C=1  TO 9:INPUT #1,T(C):NEXT:CLOSE #1:KV=(ABS(JX-DIX)<ABS(JY-DIY))+1
165 GOSUB 105
170 X=SA:Y=SB:GOSUB 100
175 GOSUB 30:IF SU>19 AND SU<24 THEN GOSUB 188:GOTO 215 ELSE IF SU=13 THEN 270
177 IF A$="q" THEN 85 ELSE IF A$="s" THEN 125 ELSE IF SU=28 THEN 119 ELSE IF A$="o" THEN 140
181 GOTO 175
188 GOSUB 100
190 ON SU-19 GOSUB 195,200,205,210
192 RETURN
195 SB=SB-1+8*(SB=0):RETURN
200 SB=SB+1-8*(SB=7):RETURN
205 SA=SA+1-20*(SA=19):RETURN
210 SA=SA-1+20*(SA=0):RETURN
215 GOSUB 105
216 ON Z(SA,SB)+1 GOSUB 219,220,225,230,235,240,245,250,255,260
217 PRINT C$;:GOSUB 800:GOTO 170
219 C$="":RETURN
220 C$="树木->":RETURN
225 C$="金矿->":RETURN
230 C$="山地":RETURN
235 C$="基地 L->":RETURN
240 C$="敌基地 HP->":RETURN
245 C$="野怪 HP->":RETURN
250 C$="步兵 HP->":RETURN
255 C$="敌兵 HP->":RETURN
260 C$="英雄 HP->":FS=Q(SA,SB)*3:RETURN
270 GOSUB 105:ON Z(X,Y)GOTO 275,275,275,280,275,275,370,275,371
275 Y$="无效操作":GOSUB 145:GOTO 175
280 PRINT " 1.步兵 2.英雄 3.";
285 GOSUB 30:IF A$="b" THEN 288
287 IF A$="n" THEN 289 ELSE IF A$="m" THEN 360 ELSE 320
288 BA=50:BB=80:BD=7:GOTO 290
289 BA=200:BB=200:BD=9
290 IF (YI=3 AND BD=9)+(BU=15 AND BD=7)=1 THEN 285
291 GOSUB 105:IF MOA<BA OR MOB<BB THEN GOSUB 43:GOTO 175
292 PRINT "选定位置";
295 GOSUB 30:IF SU>19 AND SU<24 THEN GOSUB 188:X=SA:Y=SB:GOSUB 100 ELSE IF SU=27 THEN 320
300 IF SU<>13 THEN 295
305 IF (X>JX-2)*(X<JX+2)*(Y>JY-2)*(Y<JY+2)*(Z(X,Y)=0)=0 THEN 318
310 MOA=MOA-BA:MOB=MOB-BB:Z(X,Y)=BD:Q(X,Y)=50*(BD=7)+(BD=9)*(YI+1)
311 BU=BU+(BD=7):YI=YI+(BD=9):Z=BD-1:GOSUB 95:FS=YI*3
312 GOSUB 100:IF BD=9 THEN T(FS-2)=60:T(FS-1)=50:T(FS)=40
313 GOTO 580
318 GOSUB 44
320 GOSUB 105:GOTO 175
340 GOSUB 105:IF MOA<40 OR MOB<70 THEN GOSUB 43:GOTO 170 ELSE MOA=MOA-40:MOB=MOB-70
345 T(FS-2)=MAX:PRINT "补血成功";
346 GOSUB 100
347 GOSUB 42:GOSUB 42:GOSUB 105:GOTO 580
350 GOSUB 105:IF MOA<150 OR MOB<200 THEN GOSUB 43:GOTO 170 ELSE MOA=MOA-150:MOB=MOB-200
355 MAX=MAX+8:T(FS-1)=T(FS-1)+2:T(FS)=T(FS)+1
356 PRINT "英雄更Strong了";:GOTO 346
360 PO=FN ZF(Q(JX,JY)):IF PO>1000 THEN 285
362 GOSUB 105:IF MOB<PO OR MOA<PO THEN GOSUB 43:GOTO 175
365 MOA=MOA-PO:MOB=MOB-PO:GO=GO+4:Q(JX,JY)=Q(JX,JY)+1110:PRINT "升级完成";:GOTO 347
370 GOSUB 100:LA=1:GOTO 390
371 LA=2
373 GOSUB 100
375 PRINT "1.Move  2.  3.";
380 GOSUB 30:IF A$="b" THEN 390 ELSE IF A$="n" AND LA=2 THEN 340
385 IF A$="m" AND LA=2 THEN 350 ELSE if su>19 and su<24 then 400 else 165
390 GOSUB 105:PRINT "选定方向";
395 GOSUB 30:IF SU>19 AND SU<24 THEN 400 ELSE IF SU=27 THEN 165 ELSE 395
400 ON SU-19 GOSUB 405,410,415,420
402 GOTO 425
405 SB=SB-1+(SB=0):RETURN
410 SB=SB+1-(SB=7):RETURN
415 SA=SA+1-(SA=19):RETURN
420 SA=SA-1+(SA=0):RETURN
425 ON Z(SA,SB)+1 GOTO 430,427,428,429,429,490,515,429,517,429
427 DA=1:GOTO 435
428 DA=2:GOTO 435
429 SA=X:SB=Y:GOTO 165
430 Z(SA,SB)=Z(X,Y):Q(SA,SB)=Q(X,Y):GOSUB 110:X=SA:Y=SB:Z=6*(LA=1)+8*(LA=2):GOSUB 95
432 GOSUB 105:X=SA:Y=SB:GOSUB 100:GOTO 580
435 IF CH=15 THEN 165 ELSE IF LA=2 THEN SA=X:SB=Y:GOTO 395 ELSE GOSUB 105
436 IF DA=1 THEN D$="建立木材基地?" ELSE D$="建立采矿基地?"
437 PRINT D$;
440 GOSUB 30:IF A$="y" or a$=chr$(13) THEN 452 ELSE IF A$="n" THEN 450 ELSE 440
450 SA=X:SB=Y:GOTO 165
452 IF MOA<50 THEN GOSUB 43:GOTO 450 ELSE IF Q(SA,SB)<200+100*(DA=2) THEN 470
453 MOA=MOA-50:IF DA=1 THEN E$="木材":DB=1 ELSE E$="采矿":DB=2
454 GOSUB 105:PRINT E$;"基地已建成";:CH=CH+1:C=SA:D=SB:GOSUB 460
455 IF DA=1 THEN JMU=JMU+1:MU(JMU)=SA*100+SB ELSE JKU=JKU+1:KU(JKU)=SA*100+SB
456 SA=X:SB=Y
457 GOSUB 100:GOTO 347
460 QN=INT(RND(1)*24/SQR(CH)+2):Q(C,D)=(Q(C,D)-QN)*(Q(C,D)>QN)
461 PA=QN*(Q(C,D)>QN)+Q(C,D)*(Q(C,D)<=QN):ON DB GOTO 462,463
462 MOA=MOA+PA:GOTO 464
463 MOB=MOB+PA
464 RETURN
465 FOR CA=1  TO 50:IF MU(CA)=-1 THEN 469 ELSE C=INT(MU(CA)/100):D=MU(CA)-C*100:DB=1
466 GOSUB 460 
467 IF Q(C,D)<>0 THEN 469 ELSE BOX C*8,D*8,C*8+7,D*8+7,1,0:Z(C,D)=0:CH=CH-1
468 IF DB=1 THEN MU(CA)=-1 ELSE KU(CA)=-1
469 NEXT:RETURN
470 SA=X:SB=Y:Y$="已经建立":GOSUB 145:GOTO 165
475 FOR CA=1  TO 30:IF KU(CA)=-1 THEN 469 ELSE C=INT(KU(CA)/100):D=KU(CA)-C*100:DB=2
476 GOTO 466
477 GOSUB 30:GOSUB 30:IF MOS=0 THEN END ELSE MOS=MOS+1:CLS:PRINT "Waiting..."
478 OPEN "EmpireDATB" FOR INPUT AS #1:OPEN MIN$ FOR OUTPUT AS #2:QI=1
480 INPUT #1,XX$
481 IF MOS=QI THEN 482 ELSE FOR C=1  TO 452:INPUT #1,UT:NEXT:QI=QI+1:GOTO 480
482 CLS:PRINT XX$;:GOSUB 41:GOSUB 30:CLS:IF QI=11 THEN 85 
483 PRINT "Waiting...
484 WRITE #2,XX$:FOR AA=1  TO 452:INPUT #1,AK:WRITE #2,AK:NEXT:CLOSE #1:CLOSE #2
485 H8=0:CLS:GOTO 153
490 GOSUB 105:PRINT "进攻吗?";:SA=X:SB=Y
495 GOSUB 30:IF A$="n" THEN 165 ELSE IF A$="y" or a$=chr$(13) THEN 500 ELSE 495
500 IF FN R(2)=1 THEN GOSUB 567:GOSUB 105:GOSUB 100:GOTO 580 ELSE GOSUB 565
503 IF LA=2 THEN W1=(T(FS-1)-50)*.55+50 ELSE W1=50+GO*3/4
504 W2=INT(W1-50+FN R(7)):W2=W2*(W2>0)
505 Q(DIX,DIY)=Q(DIX,DIY)-W2:IF Q(DIX,DIY)<1 THEN PRINT "我方大获全胜!强!!";:GOTO 477
510 C=Q(DIX,DIY):J=(C<150)+(C<300)+(C<500)+1:GOSUB 100:GOTO 580
515 WW=1:GOTO 520
517 WW=2 
520 GOSUB 105:PRINT "扁它吗?";
525 GOSUB 30:IF A$="n" THEN 429 ELSE IF A$="y" or a$=chr$(13) THEN 530 ELSE 525
526 IF LA=2 THEN T(FS-2)=T(FS-2)-6*(WW=1)-FN R(5):GOTO 528
527 Q(X,Y)=Q(X,Y)-6*(WW=1)-FN R(5)
528 GOSUB 617
529 SJ=SJ+1:RETURN
530 IF FN R(3)=2 THEN GOSUB 567:GOSUB 526:SA=X:SB=Y:GOSUB 100:GOTO 580 ELSE GOSUB 565
532 GOSUB 105:IF LA=2 THEN AP=T(FS-1) ELSE AP=50+GO
533 KOU=AP-44+FN R(7):Q(SA,SB)=Q(SA,SB)-KOU
540 IF Q(SA,SB)>0 THEN 545 ELSE BOX SA*8,SB*8,SA*8+7,SB*8+7,1,0:Z(SA,SB)=0:VA=0
541 VA=VA+1:IF M(VA)=SA AND N(VA)=SB THEN M(VA)=50 ELSE IF VA<15 THEN 541
543 GOSUB 550
545 SA=X:SB=Y:GOSUB 100:GOTO 580 
550 HH=HH-(WW=2):IF LA=1 THEN 560
552 HA=FN R(5):HB=(WW=1)*FN R(2):HC=(WW=1):GOSUB 105
555 PRINT "HPmax+";HA;"  AP+";HB;"  DP+";HC;:MAX=MAX+HA:T(FS-1)=T(FS-1)+HB:T(FS)=T(FS)+HC
557 GOSUB 30
560 GOSUB 105:RETURN
565 F$="成功":GOTO 570 
567 F$="失败"
570 Y$="攻击"+F$:GOSUB 145:RETURN
580 XIN=XIN+1:IF XIN=5 THEN 140 ELSE 175
590 X=EA+H:Y=EB+I:X=X+(X=-1)-(X=20):Y=Y+(Y=-1)-(Y=8):RETURN
600 DMO=DMO+13+J*2
603 FOR C=1  TO 15
605 IF M(C)=50 THEN TT=C:GOTO 640 ELSE IF J=1 THEN 620
610 FOR H=-1  TO 1:FOR I=-1  TO 1:EA=M(C):EB=N(C):GOSUB 590
611 GOSUB 613:NEXT:NEXT:GOTO 620
613 KQ=Z(X,Y):FS=Q(X,Y)*3:IF KQ<>4 AND ABS(KQ-8)<>1 THEN 529 ELSE GOSUB 630
614 IF KQ=9 THEN RP=T(FS) ELSE RP=(KQ=4)*(30+GO/2)+(KQ=7)*(41+GO/2)
615 OO=51+J*2.3-RP+FN R(10):OO=INT(OO*(OO>0)*(FN R(3.3-J/2)=1))
616 IF KQ=9 THEN T(FS-2)=T(FS-2)-OO-INT(H8/15) ELSE Q(X,Y)=Q(X,Y)-OO-INT(H8/30)
617 GOSUB 635:IF KQ=9 THEN IF T(FS-2)<1 THEN GOSUB 110:GOTO 619
618 IF KQ=7 AND Q(X,Y)<1 THEN BU=BU-(KQ=7):GOSUB 110
619 SJ=3:RETURN
620 SJ=0
621 SA=M(C):SB=N(C):GOSUB 710:ON XC GOSUB 405,410,415,420
622 IF Z(SA,SB)<>0 THEN X=SA:Y=SB:GOSUB 613:IF SJ=3 THEN 640 ELSE 621
624 X=M(C):Y=N(C):Z(SA,SB)=8:Q(SA,SB)=Q(X,Y):GOSUB 110:Z=7:X=SA:Y=SB:M(C)=X:N(C)=Y
628 GOSUB 95:GOTO 640
630 Y$="敌人发动攻击":GOSUB 145:RETURN
635 IF Z(X,Y)=4 AND (Q(X,Y)-INT(Q(X,Y)/1000)*1000)>890 THEN 638 
637 RETURN
638 CLS:PRINT SPC(47)"你输了":GOSUB 30:GOTO 85
640 NEXT
642 IF DMO<50 OR HH=15 THEN 700 ELSE IF J>2 OR FN R(5)=4 THEN 645 ELSE 700
645 UX=50:UY=50:FOR H=-1  TO 1:FOR I=-1  TO 1:EA=DIX:EB=DIY:GOSUB 590
646 IF Z(X,Y)=0 THEN UX=X:UY=Y
648 NEXT:NEXT:IF UX=50 THEN 700
650 Y$="敌方招兵":GOSUB 145:DMO=DMO-50:Z(UX,UY)=8:Q(UX,UY)=55+INT(H8/3.5):HH=HH+1:M(TT)=UX
660 N(TT)=UY:Z=7:X=UX:Y=UY:GOSUB 95
700 SA=RX:SB=RY:GOSUB 465:GOSUB 475:GOTO 120
710 YX=JX:YY=JY:GOSUB 715:N4=P4:YX=DIX:YY=DIY:GOSUB 715:O4=P4
711 CJ=FN R(4):IF CJ=N4 THEN 711 ELSE N4=CJ
712 CJ=FN R(4):IF CJ=O4 THEN 712 ELSE O4=CJ
713 XC=(J<3)*FN R(4)+(J=3)*N4+(J=4)*O4:IF J<3 AND H8>30 THEN XC=N4
714 RETURN
715 P1=(YX<SA)*3+(YX>=SA)*4:P2=(YY<SB)*2+(YY>=SB):P4=P1*(KV=1)+P2*(KV=2):RETURN
800 ON Z(SA,SB)+1 GOSUB 805,807,808,805,820,830,830,830,830,835
805 RETURN
807 CI=0:GOTO 810
808 CI=100
810 PRINT STR$(Q(SA,SB));:IF Q(SA,SB)<200+CI THEN PRINT " 开采中...";
812 RETURN
820 PRINT STR$(INT(Q(SA,SB)/1000));" HP->";STR$(Q(SA,SB)-INT(Q(SA,SB)/1000)*1000);
822 RETURN
830 PRINT STR$(Q(SA,SB));:RETURN
835 PRINT STR$(T(FS-2));:RETURN
850 TEXT:INPUT "创建保存文件:";MIN$:IF MIN$="" THEN 850 ELSE GRAPH:PRINT "Waiting...
855 MOS=1:GOTO 478