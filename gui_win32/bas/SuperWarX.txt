1 clear:GRAPH:GOTO 7500
9 POKE 601,MU:CALL 600:RETURN
40 DIM T$(4)
50 N=4:T$(1)="开始战争":T$(2)="地图制作":T$(3)="操作简介":T$(4)="关于作者":GOSUB 100
60 ON SL GOTO 6900,4200,3650,7000:END
92 LOCATE 5,1:PRINT SPC(19);:LOCATE 5,1:RETURN
100 BOX 85,12,159,14,1,0:BOX 85,0,159,11,1,1
105 LINE 95,11,95,15:LINE 150,11,150,15:BOX 85,14,159,79,1,0
106 FOR I=16 TO N*16+16 STEP 16
107 BOX 85,15,159,I-1:LINE 87,15,87,I-1:LINE 152,15,152,I-1
108 IF I=16 THEN 110
109 IF I/16=INT(I/16) THEN M=I/16:LOCATE M,12:PRINT T$(M-1);
110 NEXT:BOX 85,14,158,I-16
120 SL=1:LA=1::BOX 88,16,151,30,1,2:BOX 85,15,159,I-17
130 V=ASC(INKEY$):SL=SL+(V=21)-(V=20):SL=SL+(SL=0)-(SL=N+1):IF V=13 THEN 170
132 IF V=27 THEN SL=N+1:GOTO 170
140 BOX 88,16*LA,151,16*LA+14,1,2:BOX 88,16*SL,151,16*SL+14,1,2:LA=SL
150 GOTO 130
170 BOX 85,14,159,79,1,0
180 RETURN
300 GOSUB 800:X=SX:Y=SY:LX=X:LY=Y
301 CLS
303 BOX 1,0,160,63,1,1:GOSUB 92:PRINT "初始化中,请稍候....";
304 FOR I=X-3 TO X+3:FOR J=Y-3 TO Y+3:XX=I:YY=J:GOSUB 900:NEXT J:NEXT I
305 XX=X:YY=Y:GOSUB 990
306 GOSUB 92:PRINT "->上下左右 输入";:GOTO 310
308 GOSUB 2200
310 V=ASC(INKEY$):IF V=13 THEN 1000
312 IF V=46 THEN X=SX:Y=SY:GOTO 370
313 IF V=32 THEN DD=1:GOSUB 992
315 IF (V>23) OR (V<14) THEN 380
316 IF V<20 THEN X=X+2*(V=14)-2*(V=19):GOTO 332 ELSE X=X+(V=22)-(V=23):Y=Y+(V=21)-(V=20)
332 IF T%(X,Y)=0 THEN X=LX:Y=LY:GOTO 310
370 XX=LX:YY=LY:GOSUB 990:XX=X:YY=Y:GOSUB 990:LX=X:LY=Y
380 IF V=27 THEN 4000
381 IF V=112 THEN 308
383 IF V=28 THEN GOSUB 92:PRINT "金:";INT(GO);" 木:";INT(TR);
384 IF V=29 THEN 4000
385 IF V=31 THEN GOSUB 6825
450 GOTO 310
700 FOR C=1 TO 200:NEXT:RETURN
701 K=M%(X,Y,0)
702 IF ID=0 THEN PRINT "->平地";:GOTO 780
709 GOTO 780
710 IF ID<>3 THEN 720
711 PRINT "->金矿:";:IF K<>2 THEN PRINT M%(X,Y,2);:IF K=1 THEN PRINT "*";:GOTO 780
719 GOTO 780
720 :
731 IF ID=5 THEN PRINT ">坛 HP:";M%(X,Y,2);:GOTO 780
732 IF ID=6 THEN PRINT "->贡坛 :";M%(X,Y,2);
780 RETURN
800 S=2335+6592-2496:WI=20:HE=8:D=8:SX=2:SY=2:DIM T%(WI+1,HE+1):JT=0:GO=50:TR=30
801 XT=0:E=20:W=160:U=4
803 IF FINAME$="" THEN GOSUB 7800:LOCATE 1,1:PRINT "Loading MAP..";:GOSUB 7760 ELSE GOSUB 3500
804 GOSUB 3572
809 GMAX=25:TMAX=15:DIM PP%(14,7)
810 GOSUB 3519
811 M%(SX,SY,1)=1:M%(SX,SY,0)=1:M%(SX,SY,2)=80:GT=1
812 TREN=0:DIM TRE%(TMAX,2):DTRA=8:GOLN=0:DIM GOL%(GMAX,2):DGOA=8:GOLN=0
850 RETURN
898 IF T%(XX,YY) THEN 949
899 REM 绘图
900 IF (XX<1) OR (XX>WI) OR (YY<1) OR (YY>HE) THEN 950
902 ID=M%(XX,YY,1):P=S+XX+YY*W
910 FOR F=0 TO 7:PX=P+E*F:POKE PX,PP%(ID,F):NEXT F
948 IF M%(XX,YY,0)=2 THEN LINE XX*D-1,(YY-1)*D,XX*D-1,YY*D-1
949 IF T%(XX,YY)=0 THEN T%(XX,YY)=1
950 RETURN
990 BOX (XX-1)*D,(YY-1)*D,XX*D-1,YY*D-1,1,2
991 RETURN
992 GOTO 1700
993 GOTO 310
996 IF XX=1 THEN BOX 1,(YY-1)*D,7,YY*D-1,0,2 ELSE BOX (XX-1)*D,(YY-1)*D,XX*D-1,YY*D-1,0,2
997 RETURN
999 REM 响应按
1000 K=M%(X,Y,1)+1:E=20:IF M%(X,Y,0)=2 THEN 1030
1001 IF K>10 THEN 1090
1002 ON K GOTO 1050,1030,1200,1300,1030,1400,1500,1600,1200,1300,1300
1030 GOSUB 92:PRINT "->无可用操作";:BEEP:IF K>10 THEN PRINT "-";M%(X,Y,2);
1031 GOTO 310
1050 GOSUB 92:PRINT "建:";
1051 REM HHHHH
1052 IF XT THEN PRINT "1心坛"; ELSE PRINT SPC(5);
1053 IF JT THEN PRINT "2祭坛"; ELSE PRINT SPC(5);
1054 IF (TM=0) AND GT THEN PRINT "3贡坛";
1055 IF TM=1 THEN PRINT "3.市场";
1056 LOCATE 5,4:V=ASC(INKEY$):IF V=27 THEN GOSUB 992:GOTO 310
1057 IF V=98 THEN 1058 ELSE IF V=110 THEN 1065 ELSE IF V=109 THEN 1075 ELSE GOTO 1052
1058 IF (GO<100) OR (TR<30) THEN GOSUB 92:PRINT "资源不够啊~";:GOTO 310
1060 IF XT<>1 THEN 1052 ELSE GOSUB 92:PRINT "建心坛[金100木30]";:GOSUB 700
1061 M%(X,Y,1)=1:M%(X,Y,2)=80:M%(X,Y,0)=1:GO=GO-100:TR=TR-30
1062 XX=X:YY=Y:GOSUB 900:Z=3:GOSUB 2000:XX=X:YY=Y:GOSUB 990:GOSUB 92:PRINT "视野扩大了~";
1063 GOSUB 700
1064 GOSUB 992:GOTO 308
1065 IF JT=0 THEN 1052
1067 IF (GO<40) OR (TR<30) THEN GOSUB 92:PRINT "资源不够啊~";:I=ASC(INKEY$):GOTO 310
1070 GOSUB 92:PRINT "建祭坛[金40木30]";:GOSUB 700
1071 X=X:Y=Y:M%(X,Y,1)=5:M%(X,Y,2)=50:M%(X,Y,0)=1:GO=GO-40:TR=TR-30:M%(X,Y,0)=1
1072 XX=X:YY=Y:GOSUB 900:Z=1:GOSUB 2000:XX=X:YY=Y:GOSUB 990
1074 GOTO 308
1075 IF GT=0 THEN 1052
1077 GOSUB 92
1078 IF TM THEN B$="林业市场[木100":GN=0:TN=100:I=7 ELSE B$="贡坛[木100":GN=60:TN=80:I=6
1079 IF (GO<GN) OR (TR<TN) THEN GOSUB 92:PRINT "资源不够啊~";:V$=INKEY$:GOTO 310
1080 PRINT "建";B$;"]";:GOSUB 700
1081 M%(X,Y,1)=I:M%(X,Y,2)=80:M%(X,Y,0)=1:M%(X,Y,0)=1:GO=GO-GN:IF I=6 THEN XT=1 ELSE JT=1
1082 TR=TR-TN:XX=X:YY=Y:GOSUB 900:Z=8-I:GOSUB 2000:XX=X:YY=Y:GOSUB 990:IF TM THEN TM=0
1084 GOTO 308
1089 GOSUB 92:PRINT "1.恢复2.升级 <跳出";:V=ASC(INKEY$):GOTO 1100
1090 REM HHHHH
1091 GOSUB 92:PRINT "请按 上下左右或跳出";:VX=ASC(INKEY$)
1092 IF VX=27 THEN GOSUB 92:GOTO 310 ELSE TX=X+(VX=22)-(VX=23):TY=Y+(VX=21)-(VX=20)
1093 IF (TX<1) OR (TX>WI) OR (TY<1) OR (TY>HE) THEN 1091
1094 K=M%(TX,TY,1):IF (K=4) OR ((M%(TX,TY,0)=2) AND (K<>0)) THEN 3299
1095 IF (K<>0) AND (K<>2) THEN 1091
1096 M%(TX,TY,1)=M%(X,Y,1):M%(TX,TY,0)=1:IF K=2 THEN TR=TR+INT(M%(TX,TY,2)*.3)
1097 M%(X,Y,1)=0:XX=X:YY=Y:GOSUB 1390:M%(TX,TY,2)=M%(X,Y,2):M%(TX,TY,0)=M%(X,Y,0)
1098 XX=TX:YY=TY:GOSUB 900:GOSUB 990:Z=1:X=TX:Y=TY:LX=XX:LY=YY:GOSUB 2000
1099 XZ=XZ+1:IF XZ<3 THEN 1091 ELSE GOTO 308
1100 IF V=27 THEN GOSUB 992:GOTO 310
1101 IF V<>98 THEN 1110 ELSE IF GO<20 THEN GOSUB 92:PRINT "黄金不足啊~";:GOTO 310
1102 GO=GO-20:M%(X,Y,2)=HNHP:GOSUB 92:PRINT "恢复完成~[耗金20]";:GOSUB 700:GOTO 308
1110 IF V<>110 THEN V=27:GOTO 1100
1111 IF HUL=0 THEN GOSUB 92:PRINT "没有升级卷啊~";:GOTO 993
1112 HUL=HUL-1:HNHP=HNHP+20:GOSUB 92:PRINT "升级完成~";:M%(X,Y,2)=HNHP:GOTO 310
1200 IF M%(X,Y,1)=8 THEN GOSUB 92:PRINT "->树木开采正常~";M%(X,Y,2);:GOTO 310
1205 GOSUB 92:PRINT "->正在前往砍树~";:OK=1:GOSUB 4500
1206 IF OK THEN M%(X,Y,1)=8:TREN=TREN+1 ELSE 308
1207 XX=X:YY=Y:GOSUB 900:GOSUB 990
1208 FOR T=1 TO TMAX:IF TRE%(T,1)=0 THEN TRE%(T,1)=X:TRE%(T,2)=Y:GOTO 1210
1209 NEXT T
1210 GOTO 308
1300 IF M%(X,Y,1)=9 THEN GOSUB 92:PRINT "->金矿开采正常~";M%(X,Y,2);:GOTO 310
1301 IF M%(X,Y,1)=10 THEN GOSUB 92:PRINT "->金矿挖空了~";:GOTO 310
1305 GOSUB 92:PRINT "->正在前往采矿~";:OK=1:GOSUB 4500
1306 IF OK THEN M%(X,Y,1)=9:GOLN=GOLN+1 ELSE 308
1307 XX=X:YY=Y:GOSUB 900:GOSUB 990
1308 FOR T=1 TO GMAX:IF GOL%(T,1)=0 THEN GOL%(T,1)=X:GOL%(T,2)=Y:GOTO 1310
1309 NEXT T
1310 GOTO 308
1390 IF XX=1 THEN BOX 1,(YY-1)*D,7,YY*D-1,1,0 ELSE BOX (XX-1)*D,(YY-1)*D,XX*D-1,YY*D-1,1,0
1391 RETURN:REM 
1400 GOSUB 92:IF HN>=HNM THEN PRINT "->英雄已全部召唤";:V$=INKEY$:GOTO 1499
1401 IF GO<HNP THEN PRINT "->黄金不足,无法召唤";:V$=INKEY$:GOTO 1499
1402 PRINT "->召唤英雄~#$%&@";:GOSUB 700
1405 HN=HN+1:GO=GO-HNP:M%(X,Y,1)=10+HN:M%(X,Y,2)=HNHP:M%(X,Y,0)=1
1408 XX=X:YY=Y:GOSUB 900:GOSUB 990:GOTO 308
1499 GOSUB 992:GOTO 310
1500 GOSUB 92:PRINT "升级: 1.英雄 2.建筑";:V=ASC(INKEY$)
1501 IF (V<>98) AND (V<>110) THEN GOSUB 992:GOTO 310
1502 IF GO<50 THEN PRINT "黄金不够啊~";:GOTO 310
1503 GO=GO-50:IF V<>98 THEN 1510
1505 HUL=HUL+1:GOSUB 92:PRINT "共有升级卷:";INT(HUL);:V$=INKEY$:GOTO 308
1510 BHP=BHP+20:IF BHP>240 THEN BHP=220:BEEP
1520 M%(SX,SY,2)=BHP:GOSUB 92:PRINT "大本营HP:";INT(BHP);:V$=INKEY$:GOTO 308
1600 GOSUB 92:PRINT "1.卖树2.买树3.退出";:V=ASC(INKEY$)
1601 IF V=109 THEN GOSUB 92:PRINT "欢迎下次光临";:GOTO 310 ELSE GOSUB 92
1602 IF V<>98 THEN 1610 ELSE IF TR<100 THEN PRINT "木材太少了";:GOTO 310
1603 TR=TR-100:GO=GO+50:PRINT "=>金";INT(GO);"木";INT(TR);:GOTO 310
1610 IF V<>110 THEN 1600 ELSE IF GO<50 THEN PRINT "黄金太少了";:GOTO 310
1611 TR=TR+90:GO=GO-50:PRINT "=>金";INT(GO);"木";INT(TR);:GOTO 310
1700 GOSUB 92:ID=M%(X,Y,1):REM 
1701 IF ID=0 THEN PRINT "->平地";:GOTO 1780
1702 IF ID>10 THEN 1750
1703 ON ID GOTO 1705,1708,1710,1715,1720,1725,1730,1735,1740,1745
1705 PRINT "->心坛 L";INT(M%(X,Y,2)/20-4);" HP:";M%(X,Y,2);:GOTO 1780
1708 PRINT "->树木:";M%(X,Y,2);"*";:GOTO 1780
1709 GOTO 1780
1710 PRINT "->金矿:";M%(X,Y,2);"*";:GOTO 1780
1711 GOTO 1780
1715 PRINT "->野兽 HP:";M%(X,Y,2);:GOTO 1780
1720 PRINT "->祭坛 HP:";M%(X,Y,2);:GOTO 1780
1725 PRINT "->贡坛 HP:";M%(X,Y,2);:GOTO 1780
1730 PRINT "->林木之井";:GOTO 1780
1735 PRINT "->砍伐中的树木";M%(X,Y,2);:GOTO 1780
1740 PRINT "->开采中的金矿";M%(X,Y,2);:GOTO 1780
1745 PRINT "->枯竭了的金矿";:GOTO 1780
1750 C=ID-10:PRINT HN$(C);" HP:";M%(X,Y,2);:GOSUB 700:IF DD=1 THEN DD=0:1089
1780 RETURN
2000 GOTO 3900
2200 XX=X:YY=Y:GOSUB 990:GOTO 5000
2201 PRINT "一天过去了~";:XZ=1
2204 IF TREN=0 THEN 2214 ELSE MM=0
2205 FOR I=TREN TO 1 STEP -1:XX=TRE%(I,1):YY=TRE%(I,2):IF (XX=0) OR (M%(XX,YY,1)<>8) THEN 2210
2206 M%(XX,YY,2)=M%(XX,YY,2)-DTRA:TR=TR+DTRA
2207 IF M%(XX,YY,2)<=0 THEN M%(XX,YY,1)=0:TREN=TREN-1 ELSE 2210
2208 BEEP:TRE%(I,1)=0:GOSUB 92:PRINT "->木场(";XX;",";YY;")枯竭";:GOSUB 700
2209 GOSUB 900:IF I<>TREN THEN GOSUB 3000
2210 NEXT I
2214 IF GOLN=0 THEN 2300
2215 FOR I=GOLN TO 1 STEP -1:XX=GOL%(I,1):YY=GOL%(I,2):IF (XX=0) OR (M%(XX,YY,1)<>9) THEN 2219
2216 M%(XX,YY,2)=M%(XX,YY,2)-DGOA:GO=GO+DGOA
2217 IF M%(XX,YY,2)<=0 THEN M%(XX,YY,1)=10:GOLN=GOLN-1 ELSE 2220
2218 BEEP:GOL%(I,1)=0:GOSUB 92:PRINT "->金矿(";XX;",";YY;")枯竭";:GOSUB 700
2219 GOSUB 900:IF I<>GOLN THEN GOSUB 3010
2220 NEXT I
2300 GOSUB 92:PRINT "#";DAY;" 金:";INT(GO);" 木:";INT(TR);
2301 XX=X:YY=Y:GOSUB 990:RETURN
3000 TRE%(I,1)=TRE%(TREN+1,1):TRE%(I,2)=TRE%(TREN+1,2):TRE%(TREN+1,1)=0:RETURN
3010 GOL%(I,1)=GOL%(GOLN+1,1):GOL%(I,2)=GOL%(GOLN+1,2):GOL%(GOLN+1,1)=0:RETURN
3155 REM HHHH
3201 DATA 36,126,126,126,60,24,255,126
3202 DATA 24,60,90,153,60,90,153,24
3203 DATA 0,8,20,34,62,83,137,255
3204 DATA 126,195,165,129,189,165,189,195
3205 DATA 24,24,255,126,255,24,255,126
3206 DATA 24,60,90,219,153,0,255,126
3211 DATA 24,126,88,126,26,126,24,60
3212 DATA 24,60,90,153,24,255,24,24,42,20,42,0,62,83,137,255,0,20,8,20,0,65,129,255
3213 DATA 130,198,238,254,186,124,56,16,0,60,90,126,66,90,66,60,42,126,169,129,169,145
3214 DATA 129,62,42,42,62,65,85,65,73,62
3250 IF H<0 THEN H=24+H
3252 IF M<0 THEN M=M+60:H=H-1
3253 IF S<0 THEN S=S+60:M=M-1
3254 CLS:PRINT "感谢使用~魔 兽","星友专用QQ:107412951","耗时:";
3255 H=INT(H):IF H>0 THEN PRINT H;"时";
3256 M=INT(M):IF M>0 THEN PRINT M;"分";
3258 S=INT(S):IF S>0 THEN PRINT S;"秒"
3259 PRINT "      抗击非典","    我们众志成城";:BOX 1,47,160,79,1,2
3260 END:REM GGGGGGG
3270 OPEN PIC$ FOR random as #1:fseek #1,NS*100-100
3274 FOR I=NS TO NS+LO:Fread #1,PS+100*(I-1),100:NEXT
3279 CLOSE #1
3280 RETURN
3299 DX=TX:DY=TY:FI=1:WX=X:WY=Y:GOSUB 3300:GOTO 310
3300 IF T%(DX,DY)=0 THEN T%(DX,DY)=1:GOSUB 900
3301 XX=DX:YY=DY:GOSUB 990:IF FI<>1 THEN XX=WX:YY=WY:GOSUB 990
3320 POKE 199,255:WHP=M%(WX,WY,2):DHP=M%(WX,WY,2):GOSUB 92
3321 PRINT "        对战";:EL=INT((DHNHP-40)/20):WL=INT((HNHP-40)/20):JJ=0
3324 TT=WHP-EL*10+(WL-EL)*5+(RND(5)-.5)*5:IF TT>WHP THEN WHP=WHP-5 ELSE WHP=INT(TT)
3325 TT=DHP-WL*10+(EL-WL)*5+(RND(5)-.5)*10:IF TT>DHP THEN DHP=DHP-5 ELSE DHP=INT(TT)
3326 JJ=JJ+1:LOCATE 5,1:PRINT "  ";INT(WHP);" <-X-> ";INT(DHP);"   ";
3327 IF (WHP<=0) AND (DHP>0) THEN WW=0:M%(WX,WY,2)=WHP:GOTO 3340
3328 IF (DHP<=0) AND (WHP>0) THEN WW=1:M%(DX,DY,2)=DHP:GOTO 3340
3329 IF (WHP<=0) AND (DHP<=0) THEN WW=1:WHP=1:GOTO 3340
3330 IF (PEEK(199)=156) AND (GO>=30) THEN GO=GO-30:WHP=HNHP
3331 IF (PEEK(199)=157) AND (RND(8)>.5) THEN GOSUB 92:PRINT "成功逃离";:GOTO 3339
3335 POKE 199,255:FOR TT=1 TO 30:NEXT TT:IF JJ<=5 THEN GOTO 3324
3336 REM
3339 REM 
3340 M%(WX,WY,2)=WHP:M%(DX,DY,2)=DHP:IF WW<>1 THEN 3447
3342 IF FI=1 THEN M%(DX,DY,2)=M%(DX,DY,2)-(HNHP/20)*8 ELSE M%(DX,DY,2)=M%(DX,DY,2)-25
3343 GOSUB 92:PRINT "战斗胜利~";:GO=GO+20:GOTO 3344
3344 IF M%(DX,DY,2)<=0 THEN PRINT "敌亡~";:DID=M%(DX,DY,1):M%(DX,DY,1)=0:M%(DX,DY,0)=0
3345 IF M%(DX,DY,2)<=0 THEN YY=DY:XX=DX:GOSUB 902
3346 GOTO 3460
3447 HP=M%(WX,WY,2):IF FI=1 THEN M%(WX,WY,2)=HP-25 ELSE M%(WX,WY,2)=HP-(DHNHP)*0.4
3448 GOSUB 92:PRINT "战斗失败~";:IF M%(WX,WY,2)<=0 THEN PRINT "战亡~"; ELSE 3460
3449 IF M%(WX,WY,1)>10 THEN HN=HN-1:M%(WX,WY,1)=0:XX=WX:YY=WY:GOSUB 900:GOTO 3610
3450 M%(WX,WY,1)=0:M%(WX,WY,0)=0:XX=WX:YY=WY:GOSUB 900:GOTO 3610
3460 IF M%(SX,SY,2)<=0 THEN LOCATE 3,1:PRINT "----  你输了~   ----":V$=INKEY$:GOTO 4120
3461 XX=DX:YY=DY:GOSUB 900:IF FI<>1 THEN XX=WX:YY=WY:GOSUB 900
3462 IF M%(DSX,DSY,2)<=0 THEN LO=4:NS=22:PS=936:GOSUB 3270:GOTO 4800
3463 RETURN
3500 OPEN FINAME$ FOR RANDOM AS#1 LEN=114
3502 FIELD #1,114 AS K$
3503 CLS:PRINT "Loading MAP..","[方向]移动光标","[输入]进行操作","[F1]查看资源 [=]察看"
3504 PRINT "[F2]系统菜单 [.]回";
3510 M%(0,0,0)=-1:J=0:PA=0:PI=1:PJ=1
3511 FOR K=0 TO 2:FOR J=0 TO 8:FOR I=0 TO 20:input #1,M%(I,J,K):next:next:next:close 1:return
3519 FOR I=1 TO 14:FOR J=0 TO 7:READ M:PP%(I,J)=M:NEXT J:NEXT I:RETURN
3572 LOCATE 1,1:PRINT "Loading DATA.....   ";
3580 HNP=40:HL=1:HNM=4:HN=0:HUL=0:GO=100:TR=100:BHP=80:TM=1:DHNHP=80:HNHP=40
3581 SX=2:DSX=WI-1
3582 IF RND(30)<.5 THEN SY=2:DSY=HE-1 ELSE SY=HE-1:DSY=2
3584 SH=PEEK(1129):SM=PEEK(1130):SS=INT(PEEK(1131/2)):DIM DH%(4,2)
3585 HN$(1)="丛林守护者":HN$(2)="动感超人":HN$(3)="恶魔猎人":HN$(4)="恐惧魔王":DAY=0
3599 LOCATE 1,1:PRINT "Loading GPE ...     ";:GOSUB 3700:GO=(5-HARD)*100:TR=(5-HARD)*100
3600 RETURN
3610 IF (WX=X) AND (WY=Y) THEN GOSUB 990:GOTO 3460 ELSE 3460
3650 PRINT "帮助~~~    魔与兽";:FOR XA=0 TO 7:GOSUB 3701:V$=INKEY$:NEXT XA:goto 1
3700 XA=INT(RND(6)*4)
3701 LOCATE 2,1:PRINT SPC(89);:LOCATE 2,1:ON XA GOTO 3702,3705,3710,3715,3720,3725,3730,3735
3702 PRINT "[P]直接结束回合","[=]回主心坛","游戏目标: 占领心坛","祭坛: 召唤英雄作战";
3703 RETURN
3705 PRINT "英雄可攻击,移动,升级,探索,砍树等","英雄可在移动中砍树, 并获得30%木材。";
3706 RETURN
3710 PRINT "[方向]移动光标","[输入]进行操作","[F1]查看资源 [=]察看","[F2]系统菜单 [.]回";
3712 RETURN
3715 PRINT "敌人辖区的英雄,建筑等的右侧由一根邪恶的竖线标示";
3716 RETURN
3720 PRINT "敌人英雄死后一回合内，死亡的位置有意念可迷惑你的英雄，不要靠近！";
3722 RETURN
3725 PRINT "打斗时等级很重要。要不断升级才行，黄金也不可以缺，树和黄金可互换";
3726 RETURN
3730 PRINT "英雄最多4个，打斗时按住F2,即有一定的逃跑概率。要注意";
3732 RETURN
3735 PRINT "敌人十分强大, 其可以用意念无限制召唤英雄,但他们受伤不能恢复"
3760 RETURN
3900 GOSUB 92:PRINT "正在展开地图,请稍候";
3901 FOR I=X-Z TO X+Z:FOR J=Y-Z TO Y+Z
3902 IF (I<1) OR (I>WI) OR (J<1) OR (J>HE) THEN 3905
3903 IF T%(I,J)=0 THEN XX=I:YY=J:GOSUB 900
3905 NEXT J:NEXT I
3920 RETURN
4000 GOSUB 92:PRINT "1.存档 2.退出程序";:V=ASC(INKEY$)
4001 IF (V<>98) AND (V<>110) THEN GOSUB 992:GOTO 310
4002 IF V<>98 THEN 4100
4003 GOTO 6700
4100 IF SAV=0 THEN GOSUB 92:PRINT "你还没存档啊,存吗?";:VX=ASC(INKEY$)
4101 IF SAV=1 THEN 4120
4102 IF VX=121 THEN 6700 ELSE IF VX=110 THEN 4120 ELSE GOSUB 992:GOTO 310
4105 H=PEEK(1129)-SH:M=PEEK(1130)-SM:S=INT(PEEK(1131)/2-SS)
4120 GOTO 3250
4132 END
4200 S=2335+6592-2496:WI=20:HE=8:D=8:
4201 E=20:W=160:CLS:PRINT "正在装载图形引擎GPE","[F1]画树","[F2]画金矿","[F3]画野兽",
4202 PRINT "[F4]存档";
4203 DIM PP%(14,7)
4205 FOR I=1 TO 14:FOR J=0 TO 7:READ M:PP%(I,J)=M:NEXT J:NEXT I
4209 X=2:Y=2:LX=X:LY=Y:CLS:BOX (X-1)*D,(Y-1)*D,X*D-1,Y*D-1,1,2
4210 V=ASC(INKEY$)
4215 IF (V>23) OR (V<14) THEN 4280
4216 X=X+(V=22)-(V=23):Y=Y+(V=21)-(V=20)
4218 X=X-(X>WI)+(X<1):Y=Y-(Y>HE)+(Y<1)
4270 XX=LX:YY=LY:GOSUB 990:XX=X:YY=Y:GOSUB 990:LX=X:LY=Y
4280 IF V=28 THEN 4401
4281 IF V=29 THEN 4402
4283 IF V=30 THEN 4403
4284 IF V=31 THEN 4300
4290 IF (V<31) AND (V>27) THEN GOSUB 990
4295 GOTO 4210
4300 CLS:PRINT "现在切不可按<跳出>","档案名(最长5 字母):"
4301 INPUT FINAME$:IF LEN(FINAME$)>5 THEN BEEP:GOTO 4300
4302 FN$="War"+FINAME$
4303 OPEN FN$ FOR RANDOM AS#1:IF LOF(1)<>0 THEN CLOSE #1:BEEP:GOTO 4320
4304 CLOSE #1
4305 OPEN FN$ FOR OUTPUT AS#1
4310 FOR K=0 TO 2:FOR J=0 TO 8:FOR I=0 TO 20:WRITE #1,M%(I,J,K):next:next:next
4318 CLOSE #1:IF GA=1 THEN 4498
4319 GOSUB 92:PRINT "OK!";:V$=INKEY$:goto 1
4320 PRINT "档案已存在.覆盖?y/n";
4321 V$=INKEY$
4322 IF V$="y" THEN GOTO 4305
4323 IF V$="n" THEN 4300
4324 GOTO 4321
4401 M%(X,Y,1)=2:M%(X,Y,2)=100:M%(X,Y,1)=2:XX=X:YY=Y:GOSUB 900:GOTO 4210
4402 M%(X,Y,1)=3:M%(X,Y,2)=100:XX=X:YY=Y:GOSUB 900:GOTO 4210
4403 M%(X,Y,1)=4:M%(X,Y,2)=80:XX=X:YY=Y:GOSUB 900:GOTO 4210
4498 RETURN
4500 FOR TA=X-1 TO X+1:FOR TB=Y-1 TO Y+1
4502 IF (TA<1) OR (TA>WI) OR (TB<1) OR (TB>HE) THEN 4510
4504 IF M%(TA,TB,1)=4 THEN OK=0:GOTO 4516
4510 NEXT TB:NEXT TA
4515 IF OK THEN GOSUB 92:PRINT "开采基地已建成";:GOSUB 700:GOTO 4520
4516 GOSUB 92:BEEP:PRINT "采矿精灵被野兽杀死~";:V$=INKEY$
4520 RETURN
4800 PLAY "L2EAP8L4AL4AO3CO2BAL1BP4L2EAP8L4AL4AO3CO2BA"
4801 PLAY "L1O3DP4O2L2EAP8L4AL4AO3CO2BAL1BP4L2EL1BP8O3L4CO2BO2GL1A":V$=INKEY$:GOTO 4120
5000 GOSUB 92:PRINT "敌军部署中...";:DAY=DAY+1:SAV=0:IF DHNHP<200 THEN DHNHP=DHNHP+HARD*2
5003 STE=STE+1:IF STE>10 THEN 5300
5009 DHNHP=80:ON STE GOTO 5010,5020,5030,5030,5800,5800,5040,5040,5040,5040
5010 FOR I=DSX-3 TO DSX+3:FOR J=DSY-3 TO DSY+3
5011 IF (I<1) OR (I>WI) OR (J<1) OR (J>HE) THEN 5013 ELSE I=I:J=J
5012 M%(I,J,0)=2:K=M%(I,J,1):IF (K<>0) AND (K<>1) THEN M%(I,J,1)=0:IF K=3 THEN M%(I,J,1)=10
5013 NEXT J:NEXT I
5014 PRINT " ";DSY;:M%(DSX,DSY,1)=1:M%(DSX,DSY,2)=160:M%(DSX,DSY,0)=2
5015 GOTO 5800
5020 TA=INT(RND(23)*3-1):TB=INT(RND(21)*3-1):IF TA+TB=0 THEN 5020
5021 M%(DSX+TA,DSY+TB,1)=7:M%(DSX+TA,DSY+TB,2)=160:M%(DSX+TA,DSY+TB,0)=2
5022 GOTO 5800
5030 TA=INT(RND(23)*7-3):TB=INT(RND(21)*7-3):IF TA+TB=0 THEN 5030
5031 TA=DSX+TA:TB=DSY+TB:IF (TA<1) OR (TA>WI) OR (TB<1) OR (TB>HE) THEN 5030
5032 IF M%(TA,TB,1)<>0 THEN 5030
5033 M%(TA,TB,1)=6:M%(TA,TB,2)=160:M%(TA,TB,0)=2
5034 IF T%(TA,TB) THEN XX=TA:YY=TB:GOSUB 900
5035 GOTO 5800
5040 TA=INT(RND(23)*18-4):TB=INT(RND(21)*9-4):IF TA+TB=0 THEN 5040
5041 TA=DSX+TA:TB=DSY+TB:IF (TA<1) OR (TA>WI) OR (TB<1) OR (TB>HE) THEN 5040
5042 IF M%(TA,TB,1)<>0 THEN 5040
5043 M%(TA,TB,1)=INT(RND(9)*4+11):M%(TA,TB,2)=DHNHP:M%(TA,TB,0)=2
5045 IF T%(TA,TB) THEN XX=TA:YY=TB:GOSUB 900
5047 TC=STE-6:DH%(TC,1)=TA:DH%(TC,2)=TB
5048 GOTO 5800
5300 REM 
5301 REM 检测周围
5302 FG=0:FOR TA=1 TO 4:DX=DH%(TA,1):DY=DH%(TA,2)
5303 IF M%(DX,DY,1)<10 THEN M%(DX,DY,1)=0:M%(DX,DY,0)=0:GOTO 5345
5304 AX=(DX<SX)-(DX>SX):AY=(DY<SY)-(DY>SY):TX=AX+DX:TY=AY+DY
5305 TB=TX:TC=TY:REM FOR TB=DX TO TX STEP AX:FOR TC=DY TO TY STEP AY
5306 IF (TB<1) OR (TB>WI) OR (TC<1) OR (TC>HE) THEN 5320
5307 ID=M%(TB,TC,1):IF ((ID<5) AND (ID<>1)) OR (M%(TB,TC,0)<>1) THEN 5320
5308 IF (M%(DX,DY,2)>20) OR ((TX=SX) AND (TY=SY)) THEN FI=2:WX=TB:WY=TC:GOSUB 3300 ELSE 5325
5309 FG=FG+1:DL%=1:IF M%(DX,DY,1)>0 THEN 5320
5310 TD=INT(RND(23)*17-5):TE=INT(RND(21)*11-5):IF TD+TE=0 THEN 5320
5311 TD=DSX+TD:TE=DSY+TE:IF (TD<1) OR (TD>WI) OR (TE<1) OR (TE>HE) THEN 5320
5312 IF M%(TD,TE,1)>0 THEN 5320
5313 M%(TD,TE,1)=DID:M%(TD,TE,2)=DHNHP:M%(TD,TE,0)=2
5315 IF T%(TD,TE)=1 THEN XX=TD:YY=TE:GOSUB 900
5317 DH%(TA,1)=TD:DH%(TA,2)=TE
5320 REM NEXT TC:NEXT TB
5324 IF (TA>HARD) OR (FG>HARD-1) THEN DL%=0:GOTO 5800:REM测试三个移动位置
5325 IF (DL%=1) AND (M%(DX,DY,2)>=25) THEN DL%=0:GOTO 5393
5326 DX=DH%(TA,1):DY=DH%(TA,2)
5327 AX=(DX<SX)-(DX>SX):AY=(DY<SY)-(DY>SY):OK=0:IF ABS(DX-SX)+ABS(DY-SY)=1 THEN 5390
5328 TB=DX+AX:TC=DY+AY:GOSUB 5360:IF OK=1 THEN 5390
5329 IF AY<>0 THEN 5336
5330 IF SY<4 THEN AY=1 ELSE AY=-1
5336 TB=DX+AX:TC=DY:GOSUB 5360:IF OK=1 THEN 5390
5337 TB=DX:TC=DY+AY:GOSUB 5360:IF OK=1 THEN 5390
5338 TB=DX+AX:TC=DY-AY:GOSUB 5360:IF OK=1 THEN 5390
5339 TB=DX:TC=DY-AY:GOSUB 5360:IF OK=1 THEN 5390
5341 GOSUB 92:PRINT "敌军英雄郁闷而死~";:DID=M%(DX,DY,1):M%(DX,DY,1)=0:M%(DX,DY,0)=0
5342 IF T%(DX,DY)=1 THEN XX=DX:YY=DY:GOSUB 900
5345 TD=INT(RND(23)*17-6):TE=INT(RND(21)*13-6):IF TD+TE=0 THEN 5345
5346 TD=DSX+TD:TE=DSY+TE:IF (TD<1) OR (TD>WI) OR (TE<1) OR (TE>HE) THEN 5345
5347 TD=TD+1-1:IF M%(TD,TE,1)<>0 THEN 5345
5348 M%(TD,TE,1)=DID:M%(TD,TE,2)=DHNHP:M%(TD,TE,0)=2
5349 DH%(TA,1)=TD:DH%(TA,2)=TE:IF T%(TD,TE)=1 THEN XX=TD:YY=TE:GOSUB 900
5350 GOTO 5390
5360 IF (TB<1) OR (TB>WI) OR (TC<1) OR (TC>HE) THEN OK=0:GOTO 5389
5361 ID=M%(TB,TC,1):IF (ID>3) OR (ID=1) OR (ID=8) OR (ID=9) THEN OK=0:GOTO 5389
5362 M%(TB,TC,1)=M%(DX,DY,1):M%(TB,TC,2)=M%(DX,DY,2):M%(TB,TC,0)=2:M%(DX,DY,1)=0
5365 M%(DX,DY,0)=0:XX=DX:YY=DY:IF T%(XX,YY) THEN GOSUB 900
5366 IF T%(TB,TC) THEN XX=TB:YY=TC:GOSUB 900
5367 OK=1:DH%(TA,1)=TB:DH%(TA,2)=TC
5389 RETURN
5390 REM TB=M%(DX,DY,2):IF TB<DHNHP THEN M%(DX,DY,2)=TB+10
5393 TB=M%(DX,DY,2):IF TB<DHNHP THEN M%(DX,DY,2)=TB+10
5394 LOCATE 5,15:PRINT INT(TA*12+50);"%";:NEXT TA
5800 LOCATE 5,15:PRINT "100%";:SAV=0:GOSUB 92
5801 GOTO 2201
6000 goto 1:CLS:PRINT "请选择:","读取: 1 2 3 4 5 6":V$=INKEY$:IF V$=CHR$(27) THEN end
6002 SNA$="War"+CHR$(10)+CHR$(2)+"MS"+V$:SNB$="War"+CHR$(10)+CHR$(2)+"DA"+V$
6003 SNC$="War"+CHR$(10)+CHR$(2)+"GR"+V$:SND$="War"+CHR$(10)+CHR$(2)+"T%"+V$
6005 OPEN SNA$ FOR RANDOMAS#1:IF LOF(1)<>0 THEN 6010
6006 CLOSE #1
6007 BEEP:PRINT "---档案不存在---":V$=INKEY$:GOTO 6000
6010 CLOSE #1
6020 S=2335:WI=20:HE=8:D=8:DIM T%(WI+1,HE+1):E=20:W=160:DIM DH%(4,2)
6021 GMAX=15:TMAX=25:DIM PP%(14,7):DIM TRE%(TMAX,2):DIM GOL%(GMAX,2):DTRA=8:DGOA=8:HNM=4
6025 U=5:K=1:OPEN SNA$ FOR RANDOMAS#1LEN=114:FIELD #1,114AS K$:K=1:GOSUB 3503
6027 LOCATE 1,1:PRINT "Loading 个人数据... ";
6030 OPEN SNB$ FOR INPUT AS#1
6032 INPUT #1,SX,SY,JT,GO,TR,XT,TREN,GOLN,HARD,HNP,HN,HUL,BHP,TM,DHNHP,HNHP,DAY,STE,X,LX,Y,LY
6033 INPUT #1,DSX,DSY,GT
6035 FOR I=1 TO TMAX:INPUT #1,TRE%(I,1),TRE%(I,2):NEXT
6036 FOR I=1 TO GMAX:INPUT #1,GOL%(I,1),GOL%(I,2):NEXT
6037 FOR I=1 TO 4:INPUT #1,DH%(I,1),DH%(I,2):NEXT
6038 CLOSE #1
6040 OPEN SND$ FOR RANDOMAS#1LEN=8
6041 FIELD #1,8AS K$:POKE 7661,8:POKE 7662,156:POKE 7663,15
6042 FOR I=1 TO 20
6043 GET #1,I:LSET P$=K$
6044 FOR J=1 TO 8:P=J+3995:T%(I,J)=PEEK(P)
6045 NEXT J:NEXT I
6046 CLOSE #1
6050 HN$(1)="丛林守护者":HN$(2)="动感超人":HN$(3)="恶魔猎人":HN$(4)="恐惧魔王"
6052 SH=PEEK(1129):SM=PEEK(1130):SS=INT(PEEK(1131/2))
6059 LOCATE 1,1:PRINT "Loading GPE ...     ";
6060 FOR I=1 TO 14:FOR J=0 TO 7:READ M:PP%(I,J)=M:NEXT J:NEXT I
6080 CLS:BOX 1,0,160,63,1,1:GOSUB 92:PRINT "初始化中,请稍候....";
6150 OPEN SNC$ FOR RANDOMAS#1LEN=100:FIELD #1,100AS K$
6155 POKE 7661,100
6156 FOR I=1 TO 14:PP=2396+I*100:PI=INT(PP/256):PJ=PP-PI*256
6158 POKE 7662,PJ:POKE 7663,PI
6159 GET #1,I:LSET P$=K$
6160 NEXT I
6165 CLOSE #1
6190 XX=X:YY=Y:GOSUB 992:GOTO 310
6200 N=3:T$(1)="  简单  ":T$(2)="  一般  ":T$(3)="  困难  ":GOSUB 100
6202 HARD=SL+1:IF SL=4 THEN 6900
6205 CLS:PRINT "请选择地图"," ","直接输入档案名，或  直接按[输入]，使用默认地图";
6207 LOCATE 2,1:INPUT FINAME$:GOTO 6508
6300 CLS:LOCATE 3,1:PRINT " 请参见压缩包内文件":V$=INKEY$:goto 1
6508 IF FINAME$="" THEN CLS:GOTO 300
6509 FINAME$="War"+FINAME$
6510 OPEN FINAME$ FOR RANDOM AS#1:IF LOF(1)<>0 THEN CLOSE #1:GOTO 300
6515 LOCATE 2,1:PRINT "---档案不存在---":V$=INKEY$:CLOSE #1:GOTO 6205
6700 gosub 992:goto 310
6701 GOSUB 92:PRINT "存档: 1 2 3 4 5 6";:V$=INKEY$:IF V$=CHR$(27) THEN GOSUB 992:GOTO 310
6705 SNA$="War"+CHR$(10)+CHR$(2)+"MS"+V$:SNB$="War"+CHR$(10)+CHR$(2)+"DA"+V$
6706 SNC$="War"+CHR$(10)+CHR$(2)+"GR"+V$:SND$="War"+CHR$(10)+CHR$(2)+"T%"+V$
6708 OPEN SNA$ FOR RANDOMAS#1:IF LOF(1)=0 THEN 6720
6709 GOSUB 92:PRINT "此存档已使用~覆盖?";:V$=INKEY$:IF V$<>"y" THEN CLOSE #1:GOTO 6700
6720 GOSUB 92:PRINT "存档中..MAP";:CLOSE #1:OPEN SNA$ FOR OUTPUTAS#1:CLOSE #1
6721 OPEN SNA$ FOR RANDOMAS#1LEN=114:FIELD #1,114AS K$
6722 PMX=8200:GA=1
6727 LOCATE 5,9:PRINT "DATA..";
6730 OPEN SNB$ FOR OUTPUTAS#1
6732 WRITE #1,SX,SY,JT,GO,TR,XT,TREN,GOLN,HARD,HNP,HN,HUL,BHP,TM,DHNHP,HNHP,DAY,STE,X,LX,Y,LY
6733 WRITE #1,DSX,DSY,GT
6734 FOR I=1 TO TMAX:WRITE #1,TRE%(I,1),TRE%(I,2):NEXT
6735 FOR I=1 TO GMAX:WRITE #1,GOL%(I,1),GOL%(I,2):NEXT
6736 FOR I=1 TO 4:WRITE #1,DH%(I,1),DH%(I,2):NEXT
6737 CLOSE #1:OPEN SND$ FOR OUTPUTAS#1:CLOSE #1
6744 OPEN SND$ FOR RANDOMAS#1LEN=8
6745 FIELD #1,8AS K$:POKE 7661,8:POKE 7662,156:POKE 7663,15
6746 FOR I=1 TO 20
6748 FOR J=1 TO 8:P=J+3995:TA%=T%(I,J):POKE P,TA%:NEXT J:LSET K$=P$:PUT #1,I
6749 NEXT I:PUT #1,21:PUT #1,22:CLOSE #1
6750 OPEN SNC$ FOR OUTPUTAS#1:CLOSE #1:OPEN SNC$ FOR RANDOMAS#1LEN=100:FIELD #1,100AS K$
6755 POKE 7661,100
6756 FOR I=1 TO 15:PP=2396+I*100:PI=INT(PP/256):PJ=PP-PI*256
6758 POKE 7662,PJ:POKE 7663,PI
6759 LSET K$=P$:PUT #1,I
6760 NEXT I
6765 CLOSE #1
6800 GOSUB 92:BEEP:PRINT "存档完成~";:SAV=1:GOSUB 700:GOSUB 992:GOTO 310
6825 POKE 25,0:V$=INKEY$:POKE 25,3:RETURN
6900 N=2:T$(1)="新战争..":T$(2)="载入存档":GOSUB 100
6901 ON SL GOTO 6200,6000,50
6902 GOTO 50
6999 REM HHH
7000 LO=4:NS=17:PS=1436-2496+6592:GOSUB 3270:V$=INKEY$:goto 1
7500 DIM M%(20,8,2):PMX=7860:PIC$="WarPic"
7501 LO=16:PS=6592:NS=1
7502 GOSUB 3270:inkey$:GOTO 50
7760 FOR I=1 TO 20:FOR J=1 TO 8
7761 IF (I=1) OR (I=20) OR (J=1) OR (J=8) THEN M%(I,J,1)=2:M%(I,J,2)=100:7770
7762 IF ((I=2) OR (I=19)) AND (RND(9)<.6) THEN M%(I,J,1)=3:M%(I,J,2)=100:GOTO 7770
7763 IF RND(8)<0.1 THEN M%(I,J,1)=2:M%(I,J,2)=100:GOTO 7770
7764 IF RND(8)<.03 THEN M%(I,J,1)=4:M%(I,J,2)=80:GOTO 7770
7765 IF I=10 THEN LOCATE 2,1:GOSUB 3700
7770 NEXT J:NEXT I
7775 RETURN
7800 CLS:LOCATE 2,1:PRINT "[方向]移动光标","[输入]进行操作","[F1]查看资源 [=]察看"
7801 PRINT "[F2]系统菜单 [.]回";
7830 RETURN