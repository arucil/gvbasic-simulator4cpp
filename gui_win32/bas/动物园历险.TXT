0 :
10 GOTO 9500
20 IK=0:IK=ASC(INKEY$)
30 IF IK=98 THEN IK=1
40 IF IK=110 THEN IK=2
50 IF IK=109 THEN IK=3
60 IF IK=103 THEN IK=4
70 IF IK=104 THEN IK=5
80 IF IK=106 THEN IK=6
100 IF (IK=0 OR IK>EK) AND IK<>27 THEN BEEP:20
110 RETURN
150 TP=SH:IF SH=0 THEN 240
160 CLS: FOR PC=1 TO LEN(S$(TP))
180 IF ASC(MID$(S$(TP),PC,1))<122 THEN T=ASC(INKEY$):CLS:210
190 PRINT MID$(S$(TP),PC,2);:PC=PC+1
210 NEXT 
220 TP=ASC(RIGHT$(S$(TP),1))-64
230 IF TP<64 THEN 160 ELSE T=ASC(INKEY$)
240 RETURN
400 TEXT :PRINT "***** 道具列表 *****"
410 FOR PC=1 TO 30
420 IF MT(PC)=1 THEN PRINT WI$(PC); ELSE IF MT(PC)=2 THEN PRINT ""; ELSE PRINT "";
430 NEXT :TX=1:TY=1
460 CW=TY*10-10+TX:PC=MT(CW)
480 LOCATE 5,1:IF PC THEN PRINT WD$(CW);"Y"; ELSE PRINT "未知道具";:500
490 IF PC=1 THEN PRINT "未使用"; ELSE PRINT "已使用";
500 print spc(19-pos(1));:LOCATE TY+1,TX*2-1
510 K=ASC(INKEY$)
520 IF K=27 THEN CW=0:660
530 IF K=20 THEN TY=TY-1:IF TY=0 THEN TY=3
535 IF K=21 THEN TY=TY+1:IF TY=4 THEN TY=1
540 IF K=23 THEN TX=TX-1:IF TX=0 THEN TX=10
550 IF K=22 THEN TX=TX+1:IF TX=11 THEN TX=1
560 IF K<>13 THEN 460
570 K=0:IF PC<>1 THEN BEEP:460
580 LOCATE 5,1:IF CW<26 THEN PRINT "扔"; ELSE PRINT "吃";
590 PRINT WD$(CW);"：<Y/N>";SPC (19-POS (1));:LOCATE ,LEN(WD$(CW))+8
600 IK=ASC(INKEY$):IF IK<>121 THEN 460
610 IF CW<26 THEN 660
620 PC=INT(CW*15+RND(1)*8-375)
630 LOCATE ,1:PRINT "体力恢复";PC;SPC (19-POS (1));:LOCATE TY+1,TX*2-1
640 MB=MB+PC:IF MB>70+MLV%*2 THEN MB=70+MLV%*2
650 T=ASC(INKEY$)
660 MT(CW)=2:GRAPH:RETURN
700 IF MM%<40 THEN SH=59:GOSUB 150: GOTO 5850
710 MM%=MM%-40:T=27: GOTO 5690
800 GRAPH
805 PRINT "荆合肼蚴裁淳退蛋桑、俦淇淋    ￥60","②热狗     ￥120","③啤酒     ￥180"
810 LOCATE 5,1:PRINT "剩余";MM%;"元钱";SPC (19-POS (1));
820 EK=3:GOSUB 20:IF IK=27 THEN 5850
830 PC=IK+26
840 IF MT(PC)=1 OR MM%<IK*60 THEN BEEP:820
850 MT(PC)=1:LOCATE ,1:PRINT "买了";WD$(PC);WI$(PC);SPC (19-POS (1));
860 PLAY "O4L20CA":MM%=MM%-IK*60:T=ASC(INKEY$): GOTO 810
900 IF MM%<70 THEN 5850
910 TEXT :PRINT "给他钱吗:<Y/N>":LOCATE 1,13
920 IK=ASC(INKEY$):GRAPH:IF IK<>121 THEN 5850
930 MM%=MM%-70:JM=JM+1:IF JM=7 THEN JM=1
940 PRINT JM$(JM);
950 T=ASC(INKEY$): GOTO 5850
1000 CLS:ZM=ZM+1:IF ZM=8 THEN ZM=1
1010 PC=1:T1=1: FOR T=1 TO LEN(ZM$(ZM))
1020 IF MID$(ZM$(ZM),T,1)<>"$" THEN 1050
1030 LOCATE T1,1:IF T1>1 THEN PRINT CHR$(162);CHR$(T1+215);
1040 PRINT MID$(ZM$(ZM),PC,T-PC);:PC=T+1:T1=T1+1
1050 NEXT 
1060 EK=4:GOSUB 20
1070 IF IK<>VAL(RIGHT$(ZM$(ZM),1)) THEN SH=62:GOSUB 150:MB=MB-40:IF MB<1 THEN 4800 ELSE 5850
1080 GOTO 5580
2500 LOCATE 1,1:PRINT "半路杀出";DD$(DI);DI$(DI);SPC (10-LEN(DD$(DI)))
2510 BOX 1,0,160,15
2520 T=0:WHILE T<28:T=T+1:WEND
2530 IF DI>4 THEN GOSUB 150
2540 IB=0:CW=0:DB=INT(DI*13+RND(1)*8+MLV%*1.7):YDB=DB
2550 CLS:PRINT "";INT(MB):LOCATE 1,8:PRINT "> VS <  ";DB
2560 LOCATE 2,3:PRINT ""SPC (12)DI$(DI)
2570 PRINT "①攻击 ②道具 ③逃跑":IF CW THEN 2710
2580 BOX 16,15,31,30,0,2:BOX 17,30-14*MB/(70+MLV%*2),30,29,1,2
2590 BOX 127,15,142,30,0,2:BOX 128,30-14*DB/YDB,141,29,1,2
2600 IK=ASC(INKEY$):IF IK<98 THEN 2600
2605 LOCATE 4,1:PRINT SPC (39);:LOCATE 4,1
2610 if ik=122 then 3010 else IF IK<>109 THEN 2650
2620 IF RND(1)>.6+DI*.03-MLV%*.05 THEN 5850 ELSE PRINT DD$(DI);"紧追不舍，逃跑失败";
2630 T=0:WHILE T<40:T=T+1:WEND
2640 GOTO 2880
2650 IF IK<>98 THEN 2700
2660 CW=0:PC=INT(RND(1)*8+11+MLV%*2)
2670 PRINT "你向";DD$(DI);
2680 IF RND(1)>.4 THEN PRINT "飞出一脚";:T1=1: ELSE PRINT "挥出拳头":T1=0
2690 GOTO 2810
2700 GOSUB 400: GOTO 2550
2710 IF CW<1 OR CW>25 THEN 2580
2720 PC=INT(CW*4+RND(1)*8+MLV%)
2730 IF CW=6 THEN PRINT DD$(DI);"被麻醉枪击中，死死定在那里";:IB=3:2580
2800 PRINT "你用";WD$(CW);"向";DD$(DI);"使劲砸去";
2810 FOR T=5 TO 14:LOCATE 2,T
2815 IF CW THEN PRINT " "WI$(CW) ELSE PRINT " "CHR$(251-T1);CHR$(208-T1*99)
2820 NEXT :LOCATE 2,15:PRINT "  "
2825 LOCATE 4,1:PRINT SPC (39);:LOCATE 4,1
2830 IF RND(1)>.96-DI*.02+MLV%*.03+CW*.03 THEN PRINT "可惜被";DD$(DI);"挡开了";:2860
2840 DB=DB-PC:PRINT DD$(DI);"费";PC;"滴血";
2850 LOCATE 1,18:PRINT DB" "
2860 T=0:WHILE T<17:T=T+1:WEND
2870 IF DB<1 THEN DD(DI)=1:3010
2880 IF IB THEN IB=IB-1:2580
2890 LOCATE 4,1:PRINT SPC (39);:LOCATE 4,1
2900 PRINT DD$(DI);"朝你扑过来";
2910 FOR T=16 TO 5 STEP -1:LOCATE 2,T:PRINT DI$(DI)" ":NEXT 
2920 LOCATE 2,5:PRINT SPC (12)DI$(DI)
2930 PC=INT(DI*2.5+RND(1)*DI)
2940 LOCATE 4,1:PRINT SPC (39);:LOCATE 4,1
2950 IF RND(1)<.1+MLV%*.02-DI*.01 THEN PRINT "你身子一斜，避了过去":2980
2960 PRINT "你费";PC;"滴血"
2970 MB=MB-PC:LOCATE 1,3:PRINT INT(MB);" "
2980 T=0:WHILE T<12:T=T+1:WEND
2990 IF MB<1 THEN 4800
3000 GOTO 2580
3010 PC=INT(DI*2.4+RND(1)*DI):MEX=MEX+PC
3020 LOCATE 4,1:PRINT "大获全胜！得到";PC;"点经验";
3030 IF DI>3 AND (CLD2=83 OR TJD=71) THEN 3070
3040 PC=INT(DI*2.5+RND(1)*8):MM%=MM%+PC
3050 PRINT PC;"元钱";
3060 GOTO 3100
3070 IF CLD2=83 THEN T=CL2 ELSE T=TJ
3080 PRINT WD$(T);
3090 MT(T)=1
3100 T=ASC(INKEY$)
3110 IF MEX<MLV%^2.2+60 THEN 3160
3130 MEX=0:MLV%=MLV%+1:MB=70+MLV%*2
3140 CLS:PRINT "***** 玩家升级 *****等级：";MLV%,"体力：";MB,"攻击力：";11+MLV%*2;"～";
3150 PRINT 19+MLV%*2;:T=ASC(INKEY$)
3160 IF DI=18 THEN 4600
3170 IF CLD2=67 THEN 5590 ELSE 5850
4600 CLS:PRINT "  I Tried So Hard,     And Got So Far"
4610 PRINT " Ｉｎ Ｔｈｅ Ｅｎｄ    It Doesn't Even       Matter ...";
4630 FOR T=0 TO 31:LINE 1,T,160,T,2:LINE 1,79-T,160,79-T,2
4640 FOR T1=1 TO 310:NEXT :NEXT 
4650 T$=INKEY$:CLS
4660 FOR T=1 TO 30
4670 IF MT(T)=0 THEN PRINT "ぃ旱谰呋姑挥姓移耄看看《官方攻略》再玩吧！":4700
4680 NEXT 
4690 PRINT "ぃ汗喜你找齐了所有道具。拜拜啦！":T$=INKEY$:CLS
4700 FOR T=1 TO 80
4710 IF INT(T/2)=T/2 THEN LINE 1,41-T/2,160,39+T/2:LINE 1,80-T/2,160,T/2-1
4720 LINE 79+T,0,81-T,79:LINE T,0,161-T,79
4730 LOCATE 3,4:PRINT " Coolsoft (R) "
4740 NEXT 
4750 FOR T=24 TO 149 STEP 2
4760 IF T<136 THEN LINE T,30,T,49,2
4770 IF T>37 THEN LINE T-14,30,T-14,49,2
4780 NEXT 
4790 END
4800 CLS:LOCATE 2,5:PRINT "你体力ú恢В眼前一黑常接着什么事都不知道了……"
4810 FOR T=0 TO 39
4820 LINE 1,T,160,T,2:LINE 1,79-T,160,79-T,2
4830 FOR T1=1 TO 310:NEXT 
4840 NEXT 
4850 T$=INKEY$: GOTO 9500
5000 CLS:BOX 1,7,160,8:BOX 1,71,160,72
5010 FOR T=1 TO 8
5020 LOCATE 2,1:PRINT SPC (60):LOCATE 2,1:PRINT NG$(T)
5030 T1=0:WHILE T1<160:T1=T1+1:WEND
5040 NEXT 
5050 TEXT :PRINT "尊姓大名：";:INPUT "";MN$
5060 IF MN$="" THEN MN$="受害者"
5070 GRAPH:
5080 MP=3:MX=8:MY=4:MB=70:MM%=20
5100 OM=0: GOTO 5840
5110 TP=MX*2+MY*20+682
5120 POKE TP,252:POKE TP+1,185:PRINT ;
5130 K=ASC(INKEY$):IF K<20 THEN 5170
5140 MB=MB+.5:IF MB>70+MLV%*2 THEN MB=70+MLV%*2
5150 TX=MX:TY=MY
5160 ON K-19 GOTO 5180,5190,5200,5210
5170 BEEP: GOTO 5870
5180 TY=MY-1:IF TY=0 THEN MY=5:MP=MP-9:5840 ELSE 5220
5190 TY=MY+1:IF TY=6 THEN MY=1:MP=MP+9:5840 ELSE 5220
5200 TX=MX+1:IF TX=11 THEN MX=1:MP=MP+1:5840 ELSE 5220
5210 TX=MX-1:IF TX=0 THEN MX=10:MP=MP-1:5840
5220 T=RND(1)
5230 IF T>.94 AND MP<73 THEN DI=INT(RND(1)*4+1):CLD2=0:TJD=0:2500
5240 IF PEEK (TY*20+TX*2+682)=32 THEN MX=TX:MY=TY:POKE TP,32:POKE TP+1,32:5110
5250 FOR T=1 TO LEN(FN$(MP)) STEP 9
5260 IF ASC(MID$(FN$(MP),T))-64<>TX THEN 5280
5270 IF ASC(MID$(FN$(MP),T+1))-64=TY THEN LSET MS$=MID$(FN$(MP),T+2,7):5300
5280 NEXT 
5290 GOTO 5170
5300 TJD=ASC(LEFT$(MS$,1)):TJ=ASC(MID$(MS$,2))-64:SH=ASC(MID$(MS$,3))-64
5310 CLD1=ASC(MID$(MS$,4,1)):CL1=ASC(MID$(MS$,5))-64
5320 CLD2=ASC(MID$(MS$,6,1)):CL2=ASC(MID$(MS$,7))-64
5330 IF TJD<>90 THEN 5370
5340 IF MZ(TJ)=2 THEN 5170
5350 IF MZ(TJ)=0 THEN SH=CL2:GOSUB 150: GOTO 5850
5360 IF TJ=1 THEN MZ(TJ)=2
5365 GOTO 5500
5370 IF TJD=76 THEN IF MLV%<TJ THEN SH=CL2:GOSUB 150: GOTO 5850
5375 IF TJD<>72 THEN 5420
5380 IF MT(TJ)<>2 OR TJ=19 OR TJ=3 THEN 5400
5390 CLS:PRINT "梗赫饫镆用到【";WD$(TJ);"】，可被我扔了！呜～只好重新玩了！";
5395 T=ASC(INKEY$): GOTO 5850
5400 IF MT(TJ)=0 THEN SH=CL2:GOSUB 150: GOTO 5850
5410 MT(TJ)=2: GOTO 5510
5420 IF TJD=84 THEN IF GT<TJ*4-2 OR GT>TJ*4+2 THEN SH=CL2:GOSUB 150: GOTO 5850
5430 IF TJD=78 THEN IF MZ(TJ) THEN 5170
5440 IF CLD1=83 THEN IF MT(CL1) THEN 5170
5450 IF CLD2=83 THEN IF MT(CL2) THEN 5170
5500 IF CLD1=77 THEN IF DD(CL1) THEN 5520 ELSE DI=CL1:2500
5510 GOSUB 150:IF CLD1=64 THEN 5850
5520 IF CLD1=85 THEN 700
5530 IF CLD1=86 THEN 800
5540 IF CLD1=87 THEN 1000
5550 IF CLD1=88 THEN 900
5560 IF CLD1=89 THEN T=1:5230
5570 IF CLD1=90 THEN MZ(CL1)=1
5580 IF CLD1<>67 AND CLD2<>67 THEN 5670
5590 IF CLD1=67 THEN PC=CL1 ELSE PC=CL2
5610 LSET MS$=MID$(CM$,PC*3-2,3)
5620 MP=ASC(MID$(MS$,1))-32
5630 MX=ASC(MID$(MS$,2))-64
5640 MY=ASC(MID$(MS$,3))-64
5650 GOTO 5840
5670 IF CLD1<>83 AND CLD2<>83 THEN 5850
5680 IF CLD1=83 THEN T=CL1 ELSE T=CL2
5690 MT(T)=1:PLAY "O4L22CA"
5700 LOCATE 1,1:PRINT "得到";WD$(T);WI$(T);SPC (20-POS (1))
5710 BOX 1,0,160,15:T=ASC(INKEY$)
5720 T=0:WHILE T<15:T=T+1:WEND: GOTO 5850
5840 GET #1,MP:GT=GT+.4:IF GT>24 THEN GT=0
5850 CLS:PRINT MID$(MP$,1,99);:POKE 803,ASC(MID$(MP$,100,1)):PRINT ;
5870 IF K<>113 THEN 5110
5880 K=0:GRAPH:PRINT "***** 系统菜单 *****①返回游戏⑤道具列表②保存进度⑥查看地图"
5890 PRINT "③载入存档 Coolsoft","④查看状态 开发小组";
5900 BOX 87,48,153,79,1,2
5910 EK=6:GOSUB 20
5920 IF IK=27 THEN 5950
5930 ON IK GOTO 5970,6030,9830,5980,5940,6150
5940 GOSUB 400: GOTO 5880
5950 TEXT :PRINT "确实退出：<Y/N>":LOCATE 1,14
5960 IK=ASC(INKEY$):IF IK=121 THEN OM=3:9760 ELSE 5880
5970 GOTO 5840
5980 CLS:PRINT "***** 玩家状态 *****大名:";MN$,"金钱:";MM%;"￥ 等级:";MLV%
5985 T=70+MLV%*2:PRINT "体力:";INT(MB);"/";T;"  ";INT(MB/T*100);"%"
5990 T=INT(MLV%^2.2+60):PRINT "经验:";MEX;"/";T;"  ";INT(MEX/T*100);"%";
6000 T=ASC(INKEY$):LOCATE 2,1:PRINT SPC (79);:LOCATE 2,1
6010 PRINT "攻击力:";MLV%*2+11;"～";MLV%*2+19
6020 IF MT(5)=1 THEN PRINT "当前游戏时间:";INT(GT);"点钟"
6025 T=ASC(INKEY$): GOTO 5880
6030 CLS:PRINT "***** 保存进度 ***** ①进度一"," ②进度二"," ③进度三"," ④进度四";:CLOSE 1
6040 EK=4:GOSUB 20:IF IK=27 THEN 5880
6050 LOCATE IK+1,11:PRINT " 保存中";
6060 OPEN "ZOOSave"+STR$(IK) FOR OUTPUTAS#1
6070 WRITE #1,MN$,MP,MX,MY,MB,MM%,MLV%,MEX,GT,JM,ZM
6080 FOR T=1 TO 30:WRITE #1,MT(T):NEXT 
6090 FOR T=1 TO 5:WRITE #1,MZ(T):NEXT 
6100 FOR T=1 TO 18:WRITE #1,DD(T):NEXT 
6110 CLOSE #1:OPEN "ZOOMap" FOR RANDOM AS#1 LEN=100:FIELD #1,100 AS MP$: GOTO 5880
6150 GRAPH:PRINT "***** 查看地图 *****"
6160 IF MP>72 OR MT(2)<>1 THEN LOCATE 3,4:PRINT "〈 未知区域 〉":6220
6170 FOR T=25 TO 133 STEP 12:LINE T,20,T,60:NEXT 
6180 FOR T=20 TO 60 STEP 5:LINE 25,T,133,T:NEXT 
6190 LOCATE 5,1:PRINT "■：当前区域";
6200 T=MP-INT((MP-1)/9)*9:T1=INT((MP-1)/9+1)
6210 BOX 14+T*12,15+T1*5,25+T*12,19+T1*5,1
6220 T=ASC(INKEY$): GOTO 5880
9500 CLEAR:CLS:PRINT " "," 【蠖物园历险记〗","   Now Loading..."," TC808版BY:lihuahui"
9510 BOX 8,8,152,70,0,2:BOX 7,33,153,46
9530 MS$="YQQ&LCJ"
9540 DIM NG$(8),MT(30),WD$(30),WI$(30),DD$(18),DI$(18),DD(18),S$(62),FN$(91)
9550 OPEN "ZOOData" FOR INPUT AS#1
9560 FOR T=1 TO 30:INPUT #1,WI$(T),WD$(T):NEXT 
9570 FOR T=1 TO 18:INPUT #1,DI$(T),DD$(T):NEXT 
9580 FOR T=1 TO 8:INPUT #1,NG$(T):NEXT 
9590 FOR T=1 TO 62:INPUT #1,S$(T):NEXT 
9600 FOR T=1 TO 6:INPUT #1,JM$(T):NEXT 
9610 FOR T=1 TO 7:INPUT #1,ZM$(T):NEXT 
9620 FOR T=1 TO 91:INPUT #1,FN$(T):NEXT 
9630 INPUT #1,CM$:CLOSE #1
9640 OPEN "ZOOMap" FOR RANDOMAS#1 LEN=100:FIELD #1,100 AS MP$
9650 GRAPH:PRINT "Adventure In The Zoo 动     叫碌目始"," 物历   妇傻幕匾"
9660 PRINT " 园险   凉赜谖颐","   记   家退江湖";
9670 BOX 1,0,160,14,1,2:BOX 1,14,47,79
9680 K=0:WHILE K<>13
9690 BOX 47,OM*16+15,160,OM*16+31,0,2
9700 K=ASC(INKEY$)
9710 BOX 47,OM*16+15,160,OM*16+31,0,2
9720 IF K=20 THEN OM=OM-1:IF OM<0 THEN OM=3
9730 IF K=21 THEN OM=OM+1:IF OM>3 THEN OM=0
9740 IF K=27 THEN OM=3:9760
9750 WEND:PLAY "O4L33BEG"
9760 IF OM=3 THEN POKE 199,141:END
9770 IF OM<>2 THEN 9820
9780 CLS:PRINT "  引擎设计：杨芹ｑ    QQ号码：77993092"
9790 PRINT "  剧情设计：梁春杰     梁可超、何岩松 "," TC808移植:lihuahui";
9810 T$=INKEY$: GOTO 9650
9820 IF OM=0 THEN 5000
9830 CLS:PRINT "***** 载入存档 ***** ①存档一"," ②存档二"," ③存档三"," ④存档四";
9840 EK=4:GOSUB 20
9850 IF IK=27 THEN IF OM=1 THEN 9650 ELSE 5880
9860 LOCATE IK+1,11:PRINT " 读取中";
9870 OPEN "ZOOSave"+STR$(IK) FOR INPUT AS#2
9880 INPUT #2,MN$,MP,MX,MY,MB,MM%,MLV%,MEX,GT,JM,ZM
9890 FOR T=1 TO 30:INPUT #2,MT(T):NEXT 
9900 FOR T=1 TO 5:INPUT #2,MZ(T):NEXT 
9910 FOR T=1 TO 18:INPUT #2,DD(T):NEXT 
9920 CLOSE #2: GOTO 5100
9990 REM Coolsoft(R)RPG -Game's Engine For GVBasic.