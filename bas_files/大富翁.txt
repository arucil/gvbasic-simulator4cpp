1 GOTO 10
2 K=ASC(INKEY$)
3 RETURN
4 LOCATE 5,1:PRINT"财政状况:$";M(P);SPC(19-POS(1));:RETURN
10 X(1)=148:Y(1)=72:X(2)=155:Y(2)=72:M(1)=5E3:M(2)=5E3:A(1)=1:A(2)=1:P=1:F4=1:DIM L(32),H(31)
30 GRAPH:PRINT"茗        啉";SPC(20);"    MONOPOLY    
60 LOCATE 5,1:PRINT"狲        ";
65 IF F4 THEN LOCATE 2,4:PRINT"$";M(1);TAB(12);"$";M(2)
90 BOX 0,0,159,79:FOR I=0 TO 1:LINE 0,I*50+15,159,I*50+15:LINE I*128+16,0,I*128+16,79
110 K=I*144:LINE K,25,K+15,25:LINE K,34,K+15,34:LINE K,55,K+15,55:LINE K,46,K+15,46
150 FOR J=0 TO 6:LINE J*16+32,I*65,J*16+32,I*65+15:NEXT:NEXT 
190 CIRCLE X(1),Y(1),2,1:BOX X(1)-1,Y(1)-1,X(1)+1,Y(1)+1,0,0:DRAW X(1),Y(1),0
200 CIRCLE X(2),Y(2),2,1:ON F4 GOSUB 3600
210 GOSUB 2:IF K=27 THEN POKE 199,155 ELSE IF K=31 THEN F4=NOT F4:30 ELSE IF K=25 GOTO 4000
270 R=INT(RND(1)*6)+1
280 BOX 75,50,85,60:IF R=1 OR R=3 OR R=5 THEN DRAW 80,55
300 IF R>1 THEN DRAW 78,57:DRAW 82,53
310 IF R>3 THEN DRAW 78,53:DRAW 82,57:IF R=6 THEN DRAW 78,55:DRAW 82,55
340 FOR I=1 TO R:CIRCLE X(P),Y(P),2,0,0:CIRCLE X(P),Y(P),1,0,0
360 IF X(P)>16 AND Y(P)>65 THEN X(P)=X(P)-16:400
370 IF X(P)<=16 AND Y(P)>15 THEN Y(P)=Y(P)-11:400
380 IF X(P)<144 AND Y(P)<=15 THEN X(P)=X(P)+16:400
390 IF X(P)>=144 AND Y(P)<=65 THEN Y(P)=Y(P)+11
400 A(P)=1+A(P)*(A(P)<30):CIRCLE X(P),Y(P),2,1:FOR K=1 TO 500:NEXT:NEXT
440 IF P=1 THEN BOX X(P)-1,Y(P)-1,X(P)+1,Y(P)+1,0,0:DRAW X(P),Y(P),0
450 RESTORE:FOR I=1 TO A(P):READ N$,C:NEXT:GOSUB 2:ON R>=A(P)GOSUB 2100
510 IF A(P)=2 OR A(P)=9 OR A(P)=17 OR A(P)=24 THEN GOSUB 3000:GOTO 1900
520 IF A(P)=5 OR A(P)=13 OR A(P)=20 OR A(P)=28 THEN GOSUB 2000:GOTO 30
530 IF A(P)=25 THEN CLS:GOSUB 3800:GOTO 1900
540 IF A(P)=8 OR A(P)=23 THEN GOSUB 2200:GOTO 1900
550 IF A(P)=10 OR A(P)=16 THEN CLS:LOCATE 3,6:PRINT N$:1900
560 ON A(P)GOTO 1900
570 IF L(I)AND L(I)-P THEN GOSUB 2700:GOTO 1910
580 ON L(I)=P GOSUB 2500:ON  NOT L(I)GOSUB 2300
1010 DATA 1,0,【机会】,0,福州,300,广州,850,机场快线,0,南宁,450,昆明,700,发电厂,0,【命运】
1070 DATA 0,路过监狱,0,重庆,400,成都,750,机场快线,0,兰州,350,西安,800,免费停车场,0,【机会】
1160 DATA 0,太原,250,北京,900,机场快线,0,天津,650,沈阳,500,水务处,0
1210 DATA 【命运】,0,被判入狱,0,济南,550,南京,600,机场快线,0,上海,950,杭州,200
1900 P=3-P
1910 GOSUB 2:IF L(11)=P THEN L(11)=0:1900
1930 IF M(1)<0 OR M(2)<0 GOTO 3400 ELSE 30
2000 CLS:LOCATE 3,2:PRINT N$;"，多抽一次";:GOTO 2
2100 CLS:LOCATE 3,2:PRINT"由此路往，得$3000
2130 GOSUB 4:GOSUB 2:M(P)=M(P)+3E3:GOSUB 4:GOTO 2
2200 CLS:LOCATE 3,3:PRINT N$;"，交费$100";
2230 GOSUB 4:GOSUB 2:M(P)=M(P)-100:GOTO 4
2300 CLS:IF P=2 AND M(P)<C+250 THEN LOCATE 3,4:PRINT"电脑未购买此地";:3
2320 PRINT"地段:";N$,"地价:$";C
2350 IF P=2 AND M(P)>C+249 THEN LOCATE 4,1:PRINT"电脑买下此地段";:2420
2360 GOSUB 4:LOCATE 4,1:PRINT"你要买吗?(Y/N)";
2370 GOSUB 2:IF K-121 AND K-13 GOTO 3
2400 IF M(P)<C THEN CLS:LOCATE 3,7:PRINT"现金不够":3
2410 LOCATE 4,1:PRINT"你已买下此地段";
2420 M(P)=M(P)-C:GOSUB 4:L(I)=P:RETURN
2500 CLS:IF P=2 AND M(2)<C+200 THEN LOCATE 3,4:PRINT"电脑未增建房屋":3
2540 PRINT"地段:";N$:IF P=2 AND M(2)>C+199 THEN LOCATE 4,1:PRINT"电脑增建房屋":2630
2550 PRINT"已建房屋:";H(I),"房屋每间:$";C,"你要增建房屋吗?(Y/N)";:GOSUB 4
2590 GOSUB 2:IF K-121 AND K-13 GOTO 3
2610 IF M(P)<C THEN CLS:LOCATE 3,7:PRINT"现金不够":3
2620 LOCATE 4,1:PRINT"已增建房屋";SPC(10);
2630 H(I)=H(I)+1:LOCATE 2,1:PRINT"已建房屋:";H(I):M(P)=M(P)-C:GOTO 4
2700 CLS:IF L(11)THEN L(11)=0:LOCATE 3,6:PRINT"对方在狱中",SPC(6);"(免租金)";:3
2720 S=C/5+H(I)*C:PRINT"地段:";N$
2740 IF  NOT H(I)THEN PRINT"空地租金$";S ELSE PRINT H(I);"间房屋租金$";S;
2750 Q=A(P)+1:IF L(Q)=L(Q+1)OR L(Q)=L(Q-1)GOTO 2800
2770 PRINT"此地段已有人买下","需付租金:$";S;:GOTO 2820
2800 PRINT N$;"与邻地属同一地主,需付双倍租金";:S=S*2
2820 GOSUB 4:M(P)=M(P)-S:GOSUB 2:GOSUB 4
2870 GOSUB 2:P=3-P:M(P)=M(P)+S:IF P=2 GOTO 3
2910 CLS:LOCATE 3,2:PRINT"你的收入增加$";S:GOTO 4
3000 CLS:R=INT(RND(1)*10):LOCATE 2,7:PRINT N$:LOCATE 4,5
3040 IF  NOT R THEN PRINT"坐飞机到南方";:X(P)=P*7+77:Y(P)=72:A(P)=5
3050 IF R=1 THEN PRINT"坐飞机到西部";:X(P)=P*7-3:Y(P)=39:A(P)=13
3060 IF R=2 THEN PRINT"坐飞机到北方";:X(P)=P*7+61:Y(P)=6:A(P)=20
3070 IF R=3 THEN PRINT"坐飞机到东部";:X(P)=P*7+141:Y(P)=39:A(P)=28
3080 IF R=4 GOTO 3800 ELSE IF R<5 GOTO 3
3100 GOSUB 4
3110 IF R=5 THEN LOCATE 3,5:M(P)=M(P)+1E3:PRINT"买彩票中大奖":LOCATE ,7:PRINT"得$1000";
3120 IF R=6 THEN LOCATE 3,5:M(P)=M(P)+500:PRINT"去商业区打工":LOCATE ,8:PRINT"得$500";
3130 IF R=7 THEN LOCATE 4,1:M(P)=M(P)-500:PRINT"蔡依林演唱会门票$500";
3140 IF R=8 THEN LOCATE 4,3:M(P)=M(P)-300:PRINT"随地大小便罚$300";
3150 IF R=9 THEN LOCATE 4,2:M(P)=M(P)-200:PRINT"去商场购物花费$200";
3170 GOSUB 2:GOTO 4
3400 CLS:IF M(1)<0 THEN LOCATE 3,2:PRINT"你破产了,沦为乞丐":END
3430 FOR I=1 TO 30:IF L(I)=1 THEN TL=TL+1:TH=TH+H(I)
3450 NEXT:PRINT"  恭喜你成为大富翁  ********************
3470 PRINT"你的财产包括:",TL;"个地段、";TH;"间房屋、","资金 $";M(1);:END
3600 IF L(4)THEN BOX 112,65,128,67,L(4)-1:LINE 112,63,112+H(4)*2,63:DRAW 112,63,0
3620 IF L(5)THEN BOX 96,65,112,67,L(5)-1:LINE 96,63,96+H(5)*2,63:DRAW 96,63,0
3630 IF L(7)THEN BOX 64,65,80,67,L(7)-1:LINE 64,63,64+H(7)*2,63:DRAW 64,63,0
3640 IF L(8)THEN BOX 48,65,64,67,L(8)-1:LINE 48,63,48+H(8)*2,63:DRAW 48,63,0
3650 IF L(12)THEN BOX 14,55,16,65,L(12)-1:LINE 18,55,18,55+H(12)*2:DRAW 18,55,0
3660 IF L(13)THEN BOX 14,46,16,55,L(13)-1:LINE 18,46,18,46+H(13)*2:DRAW 18,46,0
3670 IF L(15)THEN BOX 14,25,16,34,L(15)-1:LINE 18,25,18,25+H(15)*2:DRAW 18,25,0
3680 IF L(16)THEN BOX 14,15,16,25,L(16)-1:LINE 18,15,18,15+H(16)*2
3690 IF L(19)THEN BOX 32,13,48,15,L(19)-1:LINE 32,17,32+H(19)*2,17:DRAW 32,17,0
3700 IF L(20)THEN BOX 48,13,64,15,L(20)-1:LINE 48,17,48+H(20)*2,17:DRAW 48,17,0
3710 IF L(22)THEN BOX 80,13,96,15,L(22)-1:LINE 80,17,80+H(22)*2,17:DRAW 80,17,0
3720 IF L(23)THEN BOX 96,13,112,15,L(23)-1:LINE 96,17,96+H(23)*2,17:DRAW 96,17,0
3730 IF L(27)THEN BOX 144,15,146,25,L(27)-1:LINE 142,25-H(27)*2,142,25:DRAW 142,25,0
3740 IF L(28)THEN BOX 144,25,146,34,L(28)-1:LINE 142,34-H(28)*2,142,34:DRAW 142,34,0
3750 IF L(30)THEN BOX 144,46,146,55,L(30)-1:LINE 142,55-H(30)*2,142,55:DRAW 142,55,0
3760 IF L(31)THEN BOX 144,55,146,65,L(31)-1:LINE 142,65-H(31)*2,142,65:3 ELSE 3
3800 LOCATE 3,2:PRINT"影响市容被关进监狱":LOCATE ,7:PRINT"(停一次)";
3830 X(P)=(P-1)*7+4:Y(P)=72:A(P)=10:L(11)=P:RETURN
4000 GOSUB 2:IF K=98 THEN R=1:280 ELSE IF K=110 THEN R=2:280
4030 IF K=109 THEN R=3:280 ELSE IF K=103 THEN R=4:280
4050 IF K=104 THEN R=5:280 ELSE IF K=106 THEN R=6:280 ELSE IF K=46 THEN M(P)=M(P)+5E3
4070 GOTO 270